
# Arbeiten mit Formen (Zeichnungsobjekten)

Formen oder Zeichnungsobjekte werden durch drei verschiedene Objekte dargestellt: die  **[Shapes](f9c6548c-d028-1b70-a11c-c4b45ff19177.md)** -Auflistung, die **[ShapeRange](e1b8229c-73a0-4a77-5e00-4bcec9032260.md)** -Auflistung und das **[Shape](8f01fcd1-b7d9-5216-2de5-40fb6648a403.md)** -Objekt. Die **Shapes** -Auflistung verwenden Sie im Allgemeinen zum Erstellen von Formen zum durchgehen aller Formen in einem Arbeitsblatt; Sie verwenden das **Shape** -Objekt eine einzelne Form zu formatieren. und Sie die **ShapeRange** -Auflistung verwenden, um mehrere Shapes die gleiche Weise ändern, die Sie auf der Benutzeroberfläche mit mehreren Formen arbeiten.


## Festlegen der Eigenschaften für eine Form

Viele Formateigenschaften werden nicht durch Eigenschaften festgelegt, die direkt auf das  **Shape** - oder das **ShapeRange** -Objekt angewendet werden. Stattdessen werden verwandte Formeigenschaften unter Sekundärobjekten zusammengefasst, beispielsweise dem **FillFormat** -Objekt, das alle Eigenschaften enthält, die sich auf die Füllung der Form beziehen, oder dem **LinkFormat** -Objekt, das alle Eigenschaften enthält, die sich ausschließlich auf verknüpfte OLE-Objekte beziehen. Wenn Sie Eigenschaften für eine Form festlegen möchten, müssen Sie zuerst das Objekt zurückgeben, das den Satz verwandter Formeigenschaften darstellt, und anschließend die Eigenschaften dieses zurückgegebenen Objekts festlegen. Beispielsweise können Sie mit der **Fill** -Eigenschaft das **FillFormat** -Objekt zurückgeben und danach die **ForeColor** -Eigenschaft des **FillFormat** -Objekts so festlegen, dass für die angegebene Form die Füllfarbe des Vordergrunds bestimmt wird. Dies wird im folgenden Beispiel verdeutlicht.


```
Worksheets(1).Shapes(1).Fill.ForeColor.RGB = RGB(255, 0, 0)
```


## Gleichzeitiges Anwenden einer Eigenschaft oder Methode auf mehrere Formen

Auf der Benutzeroberfläche können Sie einige Operationen ausführen, wobei gleichzeitig mehrere Formen ausgewählt sind; beispielsweise können Sie mehrere Formen auswählen und gleichzeitig die Füllungen für die einzelnen Formen festlegen. Andere Operationen lassen sich nur ausführen, wenn eine einzelne Form markiert ist. Beispielsweise können Sie den Text in einer Form nur dann bearbeiten, wenn nur eine einzelne Form markiert ist.

Visual Basic bietet zwei Möglichkeiten, Eigenschaften und Methoden auf mehrere Formen anzuwenden. Bei beiden können Sie jeden Vorgang, den Sie mit einer einzelnen Form ausführen, auch mit einem Formbereich ausführen, unabhängig davon, ob sich der Vorgang auf der Benutzeroberfläche durchführen lässt oder nicht.


- Wenn die Operation auf der Benutzeroberfläche für mehrere ausgewählte Formen ausgeführt werden kann, können Sie die gleiche Operation in Visual Basic ausführen, indem Sie eine  **ShapeRange** -Auflistung bilden, die alle Formen enthält, mit denen Sie arbeiten möchten, und die entsprechenden Eigenschaften und Methoden direkt auf die **ShapeRange** -Auflistung anwenden.
    
- Wenn die Operation auf der Benutzeroberfläche nicht gleichzeitig für mehrere ausgewählte Formen ausgeführt werden kann, können Sie die Operation trotzdem in Visual Basic ausführen, indem Sie in der  **Shapes** -Auflistung oder in einer **ShapeRange** -Auflistung, die alle zu bearbeitenden Formen enthält, einen Schleifendurchlauf ausführen und die entsprechenden Eigenschaften und Methoden auf die einzelnen **Shape** -Objekte der Auflistung anwenden.
    
Bei vielen Eigenschaften und Formen, die sich auf das  **Shape** -Objekt und die **ShapeRange** -Auflistung anwenden lassen, tritt bei bestimmten Arten von Formen ein Fehler auf, so beispielsweise bei der **TextFrame** -Eigenschaft, wenn Sie auf eine Form angewendet wird, die keinen Text enthalten kann. Solange Sie sich nicht sicher sind, dass eine bestimmte Eigenschaft oder Methode auf jede Form in einer **ShapeRange** -Auflistung angewendet werden kann, wenden Sie sie nicht auf die **ShapeRange** -Auflistung an. Wenn Sie eine dieser Eigenschaften oder Methoden auf eine Auflistung von Formen anwenden möchten, müssen Sie zuvor die Auflistung in einer Schleife bearbeiten, um durch einen Test jeder einzelnen Form sicherzustellen, dass es sich um eine gültige Form handelt.


## Erstellen einer ShapeRange-Auflistung, die alle Formen eines Blatts enthält

Sie können ein  **ShapeRange** -Objekt erstellen, das alle **Shape** -Objekte eines Blattes enthält, indem Sie die Formen markieren und anschließend mithilfe der **ShapeRange** -Eigenschaft ein **ShapeRange** -Objekt zurückgeben, das alle markierten Formen enthält.


```
Worksheets(1).Shapes.Select 
Set sr = Selection.ShapeRange
```

In Microsoft Excel ist das Argument  ** _Index_** nicht optional für die **Range** -Eigenschaft der **Shapes** -Auflistung. Deshalb können Sie diese Eigenschaft ohne Argument nicht verwenden, um ein **ShapeRange** -Objekt zu erstellen, das alle Formen einer **Shapes** -Auflistung enthält.


## Anwenden einer Eigenschaft oder Methode auf eine ShapeRange-Auflistung

Wenn Sie auf der Benutzeroberfläche eine Operation für mehrfach markierte Formen gleichzeitig durchführen können, so gelingt sie Ihnen auch von einem Programm aus, indem Sie eine  **ShapeRange** -Auflistung bilden und anschließend darauf die entsprechenden Eigenschaften und Methoden anwenden. Im folgenden Beispiel wird ein Formbereich gebildet, der in `myDocument` die Formen "Großer Stern" und "Kleiner Stern" enthält, die mit einem frei beweglichen Farbverlauf versehen werden.


```
Set myDocument = Worksheets(1) 
Set myRange = myDocument.Shapes.Range(Array("Big Star", _ 
 "Little Star")) 
myRange.Fill.PresetGradient _ 
 msoGradientHorizontal, 1, msoGradientBrass
```

Es folgen allgemeine Richtlinien dazu, wie sich Eigenschaften und Methoden verhalten, wenn sie auf eine  **ShapeRange** -Auflistung angewendet werden.


- Das Anwenden einer Methode auf die Auflistung führt zu demselben Ergebnis wie das Anwenden der Methode auf jedes einzelne  **Shape** -Objekt dieser Auflistung.
    
- Das Festlegen eines Wertes für eine Eigenschaft der Auflistung führt zu demselben Ergebnis wie das Festlegen des Wertes für die Eigenschaft jeder einzelnen Form dieses Bereichs.
    
- Eine Eigenschaft der Auflistung, die eine Konstante zurückgibt, gibt den Wert der Eigenschaft einer einzelnen Form dieser Auflistung zurück, falls alle Formen in der Auflistung für diese Eigenschaft den gleichen Wert besitzen. Besitzen nicht alle Formen der Auflistung diesen Wert, so wird eine "Misch"-Konstante zurückgegeben.
    
- Eine Eigenschaft der Auflistung, die einen einfachen Datentyp ( **Long**, **Single** oder **String** ) zurückgibt, gibt den Wert der Eigenschaft für eine einzelne Form zurück, falls alle Formen in der Auflistung für diese Eigenschaft den gleichen Wert besitzen.
    
- Der Wert mancher Eigenschaften kann nur dann zurückgegeben oder festgelegt werden, wenn sich in der Auflistung genau eine Form befindet. Enthält die Auflistung mehr als eine Form, so tritt ein Laufzeitfehler auf. Dies ist allgemein beim Festlegen und Zurückgeben von Eigenschaften der Fall, wenn die dementsprechende Aktion auf der Benutzeroberfläche nur für eine einzelne Form möglich ist (z. B. Bearbeiten von Text in einer Form oder Bearbeiten von Punkten eines Freihandobjekts).
    
Diese Richtlinien gelten auch, wenn Sie Eigenschaften von Formen festlegen, die unter Sekundärobjekten der  **ShapeRange** -Auflistung, z. B. dem **FillFormat** -Objekt, gruppiert sind. Wenn das Sekundärobjekt Operationen darstellt, die auf der Benutzeroberfläche bei mehrfach markierten Objekten ausführbar sind, können Sie das Objekt aus der **ShapeRange** -Auflistung zurückgeben und seine Eigenschaften festlegen. Beispielsweise können Sie die **Fill** -Eigenschaft verwenden, um das **FillFormat** -Objekt zurückzugeben, das die Füllungen aller Formen in der **ShapeRange** -Auflistung darstellt. Durch das Festlegen der Eigenschaften dieses **FillFormat** -Objekts werden die gleichen Eigenschaften für alle einzelnen Formen in der **ShapeRange** -Auflistung festgelegt.


## Durchlaufen einer Shapes- oder ShapeRange-Auflistung in einer Schleife

Selbst wenn ein Vorgang auf der Benutzeroberfläche nicht gleichzeitig für mehrere Formen ausgeführt werden kann, indem diese Formen ausgewählt und anschließend der Befehl angewendet wird, können Sie dennoch die entsprechende Aktion von einem Programm aus ausführen, indem Sie in einer  **Shape** - oder **ShapeRange** -Auflistung, die die zu bearbeitenden Formen enthält, in einem Schleifendurchlauf die entsprechenden Eigenschaften und Methoden auf die einzelnen **Shape** -Objekte der Auflistung anwenden. Im folgenden Beispiel werden alle Formen in `myDocument` in einer Schleife bearbeitet, und die Vordergrundfarbe für jede AutoForm-Form wird geändert.


```
Set myDocument = Worksheets(1) 
For Each sh In myDocument.Shapes 
 If sh.Type = msoAutoShape Then 
 sh.Fill.ForeColor.RGB = RGB(255, 0, 0) 
 End If 
Next
```

Im folgenden Beispiel wird eine  **ShapeRange** -Auflistung gebildet, die alle aktuell markierten Formen im aktiven Fenster enthält, und für jede markierte Form die Vordergrundfarbe festgelegt.




```
For Each sh in ActiveWindow.Selection.ShapeRange 
 sh.Fill.ForeColor.RGB = RGB(255, 0, 0) 
Next
```


## Ausrichten, Verteilen und Gruppieren von Formen in einem Formbereich

Verwenden Sie die Methoden  **[Ausrichten](7a4e6442-6730-ab7d-93b5-4c091ada6b14.md)** und **[verteilen](cef14a4b-4d6e-758e-928a-99233f893ddc.md)**, um eine Gruppe von Formen relativ zueinander oder relativ zu dem Dokument, das sie enthält zu positionieren. Verwenden der **[Group](f0ad9b81-42ad-0ee6-d2e2-ff2a88d47a97.md)** -Methode oder der **[Regroup](d30d3064-c37e-84b0-10a6-11dcd18c593e.md)** -Methode auf eine einzelne gruppierte Form aus einer Gruppe von Formen bilden.

