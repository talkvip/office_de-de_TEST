
# Eine Zelle blinken lassen

In diesem Beispiel wird gezeigt, wie Sie die Zelle B2 auf Blatt 1 blinken lassen, indem Sie die Farbe und den Text im  **StartBlinking** -Verfahren zwischen Rot und Weiß hin- und herwechseln lassen. Das **StopBlinking** -Verfahren zeigt, wie Sie das Blinken durch Löschen des Werts der Zelle und Festlegen der **[ColorIndex ](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** -Eigenschaft auf Weiß stoppen.

 **Beispielcode bereitgestellt von:** Tom Urtis,[Atlas Programming Management](http://www.atlaspm.com/)



```
Option Explicit

Public NextBlink As Double
'The cell that you want to blink
Public Const BlinkCell As String = "Sheet1!B2"

'Start blinking
Private Sub StartBlinking()
    Application.Goto Range("A1"), 1
    'If the color is red, change the color and text to white
    If Range(BlinkCell).Interior.ColorIndex = 3 Then
        Range(BlinkCell).Interior.ColorIndex = 0
        Range(BlinkCell).Value = "White"
    'If the color is white, change the color and text to red
    Else
        Range(BlinkCell).Interior.ColorIndex = 3
        Range(BlinkCell).Value = "Red"
    End If
    'Wait one second before changing the color again
    NextBlink = Now + TimeSerial(0, 0, 1)
    Application.OnTime NextBlink, "StartBlinking", , True
End Sub

'Stop blkinking
Private Sub StopBlinking()
    'Set color to white
    Range(BlinkCell).Interior.ColorIndex = 0
    'Clear the value in the cell
    Range(BlinkCell).ClearContents
    On Error Resume Next
    Application.OnTime NextBlink, "StartBlinking", , False
    Err.Clear
End Sub

```


## Über den Autor
<a name="AboutContributor"> </a>

MVP Tom Urtis ist Gründer von Atlas Programming Management, einem Full-Service-Unternehmen für Microsoft Office- und Excel-Geschäftslösungen mit Sitz im kalifornischen Silicon Valley. Urtis verfügt über mehr als 25 Jahre Erfahrung in der Unternehmensführung und der Entwicklung von Microsoft Office-Anwendungen und ist Co-Author von "Holy Macro! It's 2,500 Excel VBA Examples".

