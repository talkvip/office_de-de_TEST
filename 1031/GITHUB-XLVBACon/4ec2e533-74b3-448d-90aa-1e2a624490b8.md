
# Auswählen eines Bereichs

Die folgenden Beispiele zeigen, wie Sie den verwendeten Bereich auswählen, der formatierte Zellen beinhaltet, die keine Daten enthalten, und wie Sie einen Datenbereich auswählen, der Zellen mit tatsächlichen Daten enthält.

 **Beispielcode bereitgestellt von:** Tom Urtis,[Atlas Programming Management](http://www.atlaspm.com/)

## Auswählen des verwendeten Bereichs

In diesem Beispiel wird gezeigt, wie Sie den verwendeten Bereich auf dem aktuellen Blatt auswählen, der formatierte Zellen ohne Daten beinhaltet, indem Sie die Eigenschaft  **[UsedRange ](f004b93c-d785-de19-1fb4-bbe0b2e9b6cd.md)** des Objekts **[Worksheet ](182b705e-854a-81cc-a4b0-59b942de55ae.md)** und die Methode **[Select](46c12f85-fae5-15ea-3500-81ff8be49cdb.md)** des Objekts **[Range ](b8207778-0dcc-4570-1234-f130532cc8cd.md)** verwenden. Dann wird dem Benutzer die Adresse des Bereichs angezeigt.


```
Sub SelectUsedRange()
    ActiveSheet.UsedRange.Select
    MsgBox "The used range address is " &amp; ActiveSheet.UsedRange.Address(0, 0) &amp; ".", 64, "Used range address:"
End Sub
```


## Auswählen eines Datenbereichs beginnend mit Zelle A1

In diesem Beispiel wird gezeigt, wie Sie einen Datenbereich auf dem aktuellen Blatt, beginnend bei Zelle A1, auswählen und dem Benutzer die Adresse des Bereichs anzeigen. Der Datenbereich umfasst keine Zellen, die formatiert sind und keine Daten enthalten. Zum Abrufen des Datenbereichs findet dieses die letzte Zeile und die letzte Spalte, die tatsächliche Daten enthalten, indem die Methode  **[Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)** des Objekts **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** verwendet wird.


```
Sub SelectDataRange()
    Dim LastRow As Long, LastColumn As Long
    LastRow = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByRows, SearchDirection:=xlPrevious).Row
    LastColumn = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByColumns, SearchDirection:=xlPrevious).Column
    Range("A1").Resize(LastRow, LastColumn).Select
    MsgBox "The data range address is " &amp; Selection.Address(0, 0) &amp; ".", 64, "Data-containing range address:"
End Sub
```


## Auswahl eines Datenbereichs mit unbekannter Startposition

In diesem Beispiel wird gezeigt, wie Sie einen Datenbereich auf dem aktuellen Blatt auswählen, wenn Sie die Startposition nicht kennen, und dem Benutzer die Adresse des Bereichs anzeigen. Der Datenbereich umfasst keine Zellen, die formatiert sind und keine Daten enthalten. Zum Abrufen des Datenbereichs findet dieses die erste und letzte Zeile und Spalte, die tatsächliche Daten enthalten, indem die Methode  **[Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)** des Objekts **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** verwendet wird.


```
Sub UnknownRange()
    If WorksheetFunction.CountA(Cells) = 0 Then
        MsgBox "There is no range to be selected.", , "No cells contain any values."
        Exit Sub
    Else
        Dim FirstRow&amp;, FirstCol&amp;, LastRow&amp;, LastCol&amp;
        Dim myUsedRange As Range
        FirstRow = Cells.Find(What:="*", SearchDirection:=xlNext, SearchOrder:=xlByRows).Row
        
        On Error Resume Next
        FirstCol = Cells.Find(What:="*", SearchDirection:=xlNext, SearchOrder:=xlByColumns).Column
        If Err.Number <> 0 Then
            Err.Clear
            MsgBox _
            "There are horizontally merged cells on the sheet" &amp; vbCrLf &amp; _
            "that should be removed in order to locate the range.", 64, "Please unmerge all cells."
            Exit Sub
        End If
        
        LastRow = Cells.Find(What:="*", SearchDirection:=xlPrevious, SearchOrder:=xlByRows).Row
        LastCol = Cells.Find(What:="*", SearchDirection:=xlPrevious, SearchOrder:=xlByColumns).Column
        Set myUsedRange = Range(Cells(FirstRow, FirstCol), Cells(LastRow, LastCol))
        myUsedRange.Select
        MsgBox "The data range on this worksheet is " &amp; myUsedRange.Address(0, 0) &amp; ".", 64, "Range address:"
    End If
End Sub
```


## Über den Autor
<a name="AboutContributor"> </a>

MVP Tom Urtis ist Gründer von Atlas Programming Management, einem Full-Service-Unternehmen für Microsoft Office- und Excel-Geschäftslösungen mit Sitz im kalifornischen Silicon Valley. Urtis verfügt über mehr als 25 Jahre Erfahrung in der Unternehmensführung und der Entwicklung von Microsoft Office-Anwendungen und ist Co-Author von "Holy Macro! It's 2,500 Excel VBA Examples".

