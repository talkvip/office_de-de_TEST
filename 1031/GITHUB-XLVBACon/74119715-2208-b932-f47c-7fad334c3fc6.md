
# Verweisen auf benannte Bereiche

Bereiche lassen sich leichter durch Namen als durch die A1-Schreibweise identifizieren. Um einen markierten Bereich mit einem Namen zu versehen, klicken Sie auf das Namenfeld am linken Ende der Bearbeitungsleiste, geben einen Namen ein und drücken die EINGABETASTE.


## Verweisen auf einen benannten Bereich

Im folgenden Beispiel wird auf den Bereich mit Namen "MyRange" in der Arbeitsmappe "MyBook.xls" verwiesen.


```
vba
Sub FormatRange() 
    Range("MyBook.xls!MyRange").Font.Italic = True 
End Sub
```

Im folgenden Beispiel wird auf den tabellenblattspezifischen Bereich mit Namen "Sheet1!Sales" in der Arbeitsmappe "Report.xls" verwiesen.




```
vba
Sub FormatSales() 
    Range("[Report.xls]Sheet1!Sales").BorderAround Weight:=xlthin 
End Sub
```

Wenn Sie einen benannten Bereich markieren möchten, verwenden Sie die  **GoTo** -Methode. Sie aktiviert die Arbeitsmappe sowie das Arbeitsblatt und markiert anschließend den Bereich.




```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyBook.xls!MyRange" 
    Selection.ClearContents 
End Sub
```

Das folgende Beispiel zeigt, wie die gleiche Prozedur für die aktive Arbeitsmappe geschrieben würde.




```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyRange" 
    Selection.ClearContents 
End Sub
```

 **Beispielcode bereitgestellt von:** Dennis Wallentin,[VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)

In diesem Beispiel wird ein benannter Bereich als Formel für die Datenüberprüfung verwendet. Das Beispiel erfordert die Überprüfungsdaten in Tabelle2 im Bereich A2:A100. Diese Überprüfungsdaten dienen zum Überprüfen von Daten, die in Tabelle1 im Bereich D2:D10 eingegeben wurden.




```
vba
Sub Add_Data_Validation_From_Other_Worksheet()
'The current Excel workbook and worksheet, a range to define the data to be validated, and the target range
'to place the data in.
Dim wbBook As Workbook
Dim wsTarget As Worksheet
Dim wsSource As Worksheet
Dim rnTarget As Range
Dim rnSource As Range

'Initialize the Excel objects and delete any artifacts from the last time the macro was run.
Set wbBook = ThisWorkbook
With wbBook
    Set wsSource = .Worksheets("Sheet2")
    Set wsTarget = .Worksheets("Sheet1")
    On Error Resume Next
    .Names("Source").Delete
    On Error GoTo 0
End With

'On the source worksheet, create a range in column A of up to 98 cells long, and name it "Source".
With wsSource
    .Range(.Range("A2"), .Range("A100").End(xlUp)).Name = "Source"
End With

'On the target worksheet, create a range 8 cells long in column D.
Set rnTarget = wsTarget.Range("D2:D10")

'Clear out any artifacts from previous macro runs, then set up the target range with the validation data.
With rnTarget
    .ClearContents
    With .Validation
        .Delete
        .Add Type:=xlValidateList, _
             AlertStyle:=xlValidAlertStop, _
             Formula1:="=Source"
        
'Set up the Error dialog with the appropriate title and message
        .ErrorTitle = "Value Error"
        .ErrorMessage = "You can only choose from the list."
    End With
End With

End Sub
```


## Durchlaufen von Zellen in einem benannten Bereich in einer Schleife

Im folgenden Beispiel wird jede Zelle in einem benannten Bereich in einer Schleife durchlaufen. Hierzu wird die  **For Each...Next** -Schleife verwendet. Überschreitet der Wert einer Zelle in dem Bereich den Wert von `Limit`, wird die Farbe der Zelle in Gelb geändert.


```
vba
Sub ApplyColor() 
    Const Limit As Integer = 25 
    For Each c In Range("MyRange") 
        If c.Value > Limit Then 
            c.Interior.ColorIndex = 27 
        End If 
    Next c 
End Sub
```


## Über den Autor
<a name="AboutContributor"> </a>

Dennis Wallentin ist Autor des Blogs VSTO &amp; .NET &amp; Excel, dessen Schwerpunkt auf .NET Framework-Lösungen für Excel und Excel Services liegt. Dennis entwickelt Excel-Lösungen seit über 20 Jahren und ist Co-Autor von "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)."

