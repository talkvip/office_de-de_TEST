
# Hervorheben der aktiven Zelle, Zeile oder Spalte

Die folgenden Codebeispiele veranschaulichen Möglichkeiten zum Hervorheben der aktiven Zelle oder die Zeilen und Spalten, die die aktive Zelle enthalten. In diesen Beispielen verwenden Sie das  **[SelectionChange](183e2ca7-06b2-f689-1f77-182dbfbf1e1d.md)** -Ereignis des **[Worksheet](182b705e-854a-81cc-a4b0-59b942de55ae.md)** -Objekts.

 **Beispielcode bereitgestellt von:** Tom Urtis,[Atlas Programming Management](http://www.atlaspm.com/)

## Hervorheben der aktiven Zelle

Im folgenden Codebeispiel wird die Farbe in allen Zellen des Arbeitsblatts gelöscht, in dem die  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** -Eigenschaft auf 0 festgelegt wird. Dann wird die aktive Zelle durch Festlegen der **ColorIndex**-Eigenschaft auf 8 (türkis) hervorgehoben.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    ' Highlight the active cell
    Target.Interior.ColorIndex = 8
    Application.ScreenUpdating = True
End Sub
```


## Hervorheben der gesamte Zeile und Spalte, die die aktive Zelle enthalten

Im folgenden Codebeispiel wird die Farbe in allen Zellen des Arbeitsblatts gelöscht, in dem die  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** -Eigenschaft auf 0 festgelegt wird. Dann werden die gesamte Zeile und Spalte mit der aktiven Zelle mithilfe der Eigenschaften **[EntireRow](9e66da51-6cef-4109-ea4e-2acaad42aa1f.md)** und **[EntireColumn](7be55670-75fd-fb02-dc1a-9d70e3a9d80d.md)** hervorgehoben.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    If Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    With Target
        ' Highlight the entire row and column that contain the active cell
        .EntireRow.Interior.ColorIndex = 8
        .EntireColumn.Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## Hervorheben der Zeile und Spalte, die die aktive Zelle enthalten, innerhalb der aktuellen Region

Im folgenden Codebeispiel wird die Farbe in allen Zellen des Arbeitsblatts gelöscht, in dem die  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** -Eigenschaft auf 0 festgelegt wird. Dann werden die Zeile und Spalte mit der aktiven Zelle innerhalb der aktuellen Region mithilfe der **[CurrentRegion](39277cc5-07ff-8453-7330-b272b365f9dc.md)** -Eigenschaft des **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** -Objekts hervorgehoben.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    If IsEmpty(Target) Or Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    With ActiveCell
        ' Highlight the row and column that contain the active cell, within the current region
        Range(Cells(.Row, .CurrentRegion.Column), Cells(.Row, .CurrentRegion.Columns.Count + .CurrentRegion.Column - 1)).Interior.ColorIndex = 8
        Range(Cells(.CurrentRegion.Row, .Column), Cells(.CurrentRegion.Rows.Count + .CurrentRegion.Row - 1, .Column)).Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## Über den Autor
<a name="AboutContributor"> </a>

MVP Tom Urtis ist Gründer von Atlas Programming Management, einem Full-Service-Unternehmen für Microsoft Office- und Excel-Geschäftslösungen mit Sitz im kalifornischen Silicon Valley. Urtis verfügt über mehr als 25 Jahre Erfahrung in der Unternehmensführung und der Entwicklung von Microsoft Office-Anwendungen und ist Co-Author von "Holy Macro! It's 2,500 Excel VBA Examples".

