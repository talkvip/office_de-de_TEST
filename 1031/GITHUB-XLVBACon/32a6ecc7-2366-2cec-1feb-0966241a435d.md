
# Range.Cells Property (Excel)

Returns a  **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** object that represents the cells in the specified range.


## Syntax

 _Ausdruck_. **Cells**

 _Ausdruck_ A variable that represents a **Range** object.


## Remarks

Because the  **[Item](f7d40273-5069-8a9d-14ee-19df225f864c.md)** property is the default property for the **Range** object, you can specify the row and column index immediately after the **Cells** keyword. For more information, see the **Item** property and the examples for this topic.

Using this property without an object qualifier returns a  **Range** object that represents all the cells on the active worksheet.


## Example

This example sets the font style for cells A1:C5 on Sheet1 to italic.


```
Worksheets("Sheet1").Activate 
Range(Cells(1, 1), Cells(5, 3)).Font.Italic = True
```

This example scans a column of data named "myRange." If a cell has the same value as the cell immediately above it, the example displays the address of the cell that contains the duplicate data.




```
Set r = Range("myRange") 
For n = 1 To r.Rows.Count 
    If r.Cells(n, 1) = r.Cells(n + 1, 1) Then 
        MsgBox "Duplicate data in " &amp; r.Cells(n + 1, 1).Address 
    End If 
Next n
```

 **Sample code provided by:** Holy Macro! Books,[Holy Macro! It's 2,500 Excel VBA Examples](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)

This example looks through column C, and for every cell that has a comment, it puts the comment text into column D and deletes the comment from column C.




```
Sub SplitComments()
   'Set up your variables
   Dim cmt As Comment
   Dim iRow As Integer
   
   'Go through all the cells in Column C, and check to see if the cell has a comment.
   For iRow = 1 To WorksheetFunction.CountA(Columns(3))
      Set cmt = Cells(iRow, 3).Comment
      If Not cmt Is Nothing Then
      
         'If there is a comment, paste the comment text into column D and delete the original comment.
         Cells(iRow, 4) = Cells(iRow, 3).Comment.Text
         Cells(iRow, 3).Comment.Delete
      End If
   Next iRow
End Sub
```


## About the Contributor
<a name="AboutContributor"> </a>

Holy Macro! Books veröffentlicht unterhaltsame Bücher für Benutzer von Microsoft Office. Den kompletten Katalog finden Sie unter MrExcel.com.


## Siehe auch
<a name="AboutContributor"> </a>


#### Konzepte


[Range Object](b8207778-0dcc-4570-1234-f130532cc8cd.md)
#### Weitere Ressourcen


[Range Object Members](http://msdn.microsoft.com/library/4336bf81-1e63-7e44-1792-baf366a027a7%28Office.15%29.aspx)