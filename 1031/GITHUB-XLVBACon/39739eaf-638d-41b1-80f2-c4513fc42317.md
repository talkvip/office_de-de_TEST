
# Hier finden Sie alle Sparklines in einem Blatt

Im folgenden Codebeispiel wird mithilfe ein Listenfelds in einem Benutzerformular alle der sparklinegruppen auf dem aktiven Blatt angezeigt. Wenn Sie eine der sparklinegruppen im Listenfeld klicken, wird die Sparklinegruppe auf dem Blatt ausgewählt.

Dieses Beispiel erfordert ein Benutzerformular mit dem Namen  **SparklineForm**, einem Listenfeld auf dem Benutzerformular namens **SparklineListBox**, und eine Schaltfläche auf dem Benutzerformular namens **CloseBtn**.

Fügen Sie in Visual Basic-Editor ein  **Modul** und kopieren und fügen Sie den folgenden Code. Dieser Code zeigt das Benutzerformular.




```
Sub ShowUserForm()
    SparklineForm.Show
End Sub
```

In Visual Basic-Editor mit der rechten Maustaste in des Formulars  **SparklineForm**, wählen Sie **Code anzeigen**, und kopieren und fügen Sie den folgenden Code.
Das Verfahren  **UserForm_Activate** durchlaufen und alle sparklinegruppen auf das aktive Blatt und ruft die Adressen der sparklinegruppen mithilfe von die[Address](aaa2432e-9bb1-4a48-3868-86455bc53938.md) -Eigenschaft des[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md) -Objekts ab. Anschließend wird die Adresse im Listenfeld hinzugefügt.
Die  **SparklineListBox_Click** -Prozedur wird aufgerufen, wenn Sie die Adresse des eine Sparklinegruppe im Listenfeld klicken. Dieses Verfahren aktiviert die ausgewählten Sparklinegruppe auf dem Blatt mithilfe der[Activate](a0050055-84e7-7611-a961-887fcb063369.md) -Methode des[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md) -Objekts.
Die  **CloseBtn_Click** -Prozedur wird aufgerufen, wenn Sie klicken auf die Schaltfläche auf dem Benutzerformular und dem Schließen des Formulars.



```
Private Sub UserForm_Activate()
    'The sparkline group
    Dim oSparkGroup As SparklineGroup
    
    'Loop through all the sparkline groups on the sheet
    For Each oSparkGroup In ActiveSheet.Range("A:XFD").SparklineGroups
        'For each sparkline group found, add the address to the listbox
        SparklineListBox.AddItem oSparkGroup.Location.Address(, , , True)
    Next oSparkGroup
End Sub

Private Sub SparklineListBox_Click()
    'Activate the selected range that has the sparklines
    Range(SparklineListBox.Value).Activate
End Sub

Private Sub CloseBtn_Click()
    'Close the userform
    Unload Me
End Sub
```


## Siehe auch


#### Konzepte


[SparklineGroup-Objekt](cc694d97-a3d3-3473-2e37-0ede67b97680.md)
#### Weitere Ressourcen


[SparklineGroup-Member](http://msdn.microsoft.com/library/dad308ee-d69b-748d-d0c8-ad63c643808f%28Office.15%29.aspx)
[Programmieren mit Sparklines In Excel](http://msdn.microsoft.com/library/e26f3356-882e-44d5-94a5-c7e8d1026d78%28Office.15%29.aspx)