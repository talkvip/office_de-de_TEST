
# Verhindern, dass doppelte Einträge in einem Bereich

Im folgenden Codebeispiel wird überprüft, ob in den Bereich a1: B20 eingegebene Wert innerhalb des Bereichs auf eines der Arbeitsblätter in der aktuellen Arbeitsmappe vorhanden ist und verhindert, dass doppelte Einträge, wenn der Wert vorhanden ist.

 **Beispielcode stammt von:** Holy Macro! Bücher,[Holy Macro! Es ist's 2,500 Excel VBA-Beispiele](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)



```
Private Sub Workbook_SheetChange(ByVal Sh As Object, ByVal Target As Range)

    'Define your variables.
    Dim ws As Worksheet, EvalRange As Range
    
    'Set the range where you want to prevent duplicate entries.
    Set EvalRange = Range("A1:B20")
    
    'If the cell where value was entered is not in the defined range, if the value pasted is larger than a single cell,
    'or if no value was entered in the cell, then exit the macro.
    If Intersect(Target, EvalRange) Is Nothing Or Target.Cells.Count > 1 Then Exit Sub
    If IsEmpty(Target) Then Exit Sub
    
    'If the value entered already exists in the defined range on the current worksheet, throw an
    'error message and undo the entry.
    If WorksheetFunction.CountIf(EvalRange, Target.Value) > 1 Then
        MsgBox Target.Value &amp; " already exists on this sheet."
        Application.EnableEvents = False
        Application.Undo
        Application.EnableEvents = True
    End If
    
    'Check the other worksheets in the workbook.
    For Each ws In Worksheets
        With ws
            If .Name <> Target.Parent.Name Then
                'If the value entered already exists in the defined range on the current worksheet, throw an
                'error message and undo the entry.
                If WorksheetFunction.CountIf(Sheets(.Name).Range("A1:B20"), Target.Value) > 0 Then
                    MsgBox Target.Value &amp; " already exists on the sheet named " &amp; .Name &amp; ".", _
                    16, "No duplicates allowed in " &amp; EvalRange.Address(0, 0) &amp; "."
                    Application.EnableEvents = False
                    Application.Undo
                    Application.EnableEvents = True
                    Exit For
                End If
            End If
        End With
    Next ws

End Sub
```


## Über den Autor
<a name="AboutContributor"> </a>

Holy Macro! Books veröffentlicht unterhaltsame Bücher für Benutzer von Microsoft Office. Den kompletten Katalog finden Sie unter MrExcel.com.

