
# Méthode DoCmd.SendObject (Access)

La méthode  **SendObject** exécute l'action **SendObject** dans Visual Basic.
 


## Syntaxe

*expression* . **SendObject**( ***ObjectType***, ***ObjectName***, ***OutputFormat***, ***To***, ***Cc***, ***Bcc***, ***Subject***, ***MessageText***, ***EditMessage***, ***TemplateFile*** )
 

 
*expression* Variable représentant un objet **DoCmd**.
 

 

### Paramètres



|**Nom**|**Requis/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _ObjectType_|Facultatif|**AcSendObjectType**|Constante  **[AcSendObjectType](36c260a4-69a5-becd-f9f9-7b0346c983df.md)** qui spécifie le type d'objet à envoyer.|
| _ObjectName_|Facultatif|**Variante**|Expression chaîne qui correspond au nom valide d'un objet du type sélectionné par l'argument  _ObjectType_. Si vous voulez inclure l'objet actif dans le message électronique, spécifiez le type de l'objet avec l'argument  _ObjectType_ et laissez cet argument vierge. Si vous laissez les arguments _ObjectType_ et _ObjectName_ vierges (la constante par défaut **acSendNoObject** est choisie pour l'argument _ObjectType_), Microsoft Access envoie à l'application de courrier électronique un message ne contenant pas d'objet de base de données. Si vous exécutez du code Visual Basic contenant la méthode  **SendObject** dans une base de données bibliothèque, Microsoft Access recherche l'objet de ce nom d'abord dans la base de données bibliothèque, puis dans la base de données active.|
| _OutputFormat_|Facultatif|**Variante**|Constante qui indique le format dans lequel envoyer l'objet. Les valeurs possibles sont  **acFormatHTML**, **acFormatRTF**, **acFormatSNP**, **acFormatTXT**, **acFormatXLS**, **acFormatXLSB**, **acFormatXLSX**, **acFormatXPS** et **acFormatPDF**.|
| _To_|Facultatif|**Variante**|Expression chaîne qui contient les noms des destinataires du message, qui seront insérés sur la ligne A du message électronique. Séparez les noms des destinataires spécifiés dans cet argument de ceux spécifiés dans les arguments  _Cc_ et _Ccc_ à l'aide d'un point-virgule (;) ou du séparateur de liste défini sous l'onglet **Nombre** de la boîte de dialogue **Propriétés pour Paramètres régionaux** du Panneau de configuration Windows. Si l'application de messagerie ne peut pas identifier les noms des destinataires, le message n'est pas envoyé et une erreur survient. Si vous laissez cet argument vierge, Microsoft Access vous invite à fournir les noms des destinataires.|
| _Cc_|Facultatif|**Variante**|Expression de chaîne qui répertorie les destinataires dont vous voulez insérer le nom dans la ligne  **Cc :** du message électronique. Si vous laissez cet argument vide, la ligne **Cc :** du message électronique reste vide.|
| _Bcc_|Facultatif|**Variante**|Expression de chaîne qui répertorie les destinataires dont vous voulez insérer le nom dans la ligne  **Cci :** du message électronique. Si vous laissez cet argument vide, la ligne **Cci :** du message électronique reste vide.|
| _Subject_|Facultatif|**Variante**|Expression de chaîne contenant le texte que vous voulez insérer dans la ligne  **Objet** du message électronique. Si vous laissez cet argument vide, la ligne **Objet** du message électronique reste vide.|
| _MessageText_|Facultatif|**Variante**|Expression chaîne contenant le texte à inclure dans le corps du message électronique, après l'objet. Si vous laissez cet argument vierge, l'objet est le seul élément qui est inséré dans le corps du message électronique.|
| _EditMessage_|Facultatif|**Variante**|Utilisez  **True** (1) pour ouvrir l'application de messagerie électronique immédiatement avec le message chargé afin de pouvoir modifier celui-ci. Utilisez **False** (0) pour envoyer le message sans le modifier. Si vous laissez cet argument vide, la valeur par défaut ( **True** ) est supposée.|
| _TemplateFile_|Facultatif|**Variante**|Expression chaîne qui correspond au nom complet, y compris le chemin d'accès, du fichier que vous voulez utiliser comme modèle pour un fichier HTML.|

## Remarques

Vous pouvez utiliser l'action  **SendObject** pour inclure la feuille de données, le formulaire, le rapport ou le module Microsoft Access spécifiés dans un message électronique où vous pouvez les consulter et les transmettre. Vous pouvez inclure des objets aux formats Microsoft Excel 2000 (*.xls), texte MS-DOS (*.txt), texte enrichi (*.rtf) ou HTML (*.html) dans des messages pour Microsoft Outlook, Microsoft Exchange ou d'autres applications de messagerie électronique utilisant l'interface de programmation d'application de messagerie MAPI.
 

 
Les règles suivantes s'appliquent quand vous utilisez l'action  **SendObject** pour inclure un objet de base de données dans un message électronique :
 

 

- Vous pouvez envoyer des feuilles de données de table, de requête et de formulaire. Dans l'objet inclus, tous les champs de la feuille de données apparaissent comme dans Access sauf ceux contenant des objets OLE. Les colonnes de ces champs sont incluses dans l'objet, mais les champs eux-mêmes sont vierges.
    
 
- Pour un contrôle lié à un champ  **Oui/Non** (bouton bascule, case d'option ou case à cocher), le fichier de sortie affiche la valeur 1 (Oui) ou 0 (Non).
    
 
- Pour un champ de texte lié à un champ de  **lien hypertexte**, le fichier de sortie affiche le lien hypertexte pour tous les formats de sortie, à l'exception du format Texte MS-DOS (dans ce cas, le lien hypertexte s'affiche comme du texte normal).
    
 
- Si vous envoyez un formulaire en mode Formulaire, l'objet inclus contient la feuille de données de ce formulaire.
    
 
- Si vous envoyez un rapport, les seuls contrôles inclus dans l'objet sont des zones de texte (pour les fichiers .xls), ou des zones de texte et des étiquettes (pour les fichiers .rtf, .txt et .html). Tous les autres contrôles sont ignorés. Les informations d'en-tête et de pied de page ne sont pas incluses. L'unique exception est quand vous envoyez un rapport au format Excel. Dans ce cas, une zone de texte dans un pied de groupe contenant une expression avec la fonction  **Sum** est incluse dans l'objet. Aucun autre contrôle, qu'il s'agisse d'en-tête ou de pied (et aucune fonction d'agrégation autre que **Sum**) n'est un inclus dans l'objet.
    
 
- Des sous-états sont inclus dans l'objet. Des sous-formulaires sont inclus lorsque les données sont enregistrées dans un fichier .asp, mais seulement en tant que formulaire et non en tant que feuille de données.
    
 
- Lorsque vous envoyez une feuille de données, un formulaire ou une page d'accès aux données au format HTML, un fichier .html est créé et lorsque vous envoyez un rapport au format HTML, un fichier .html est créé pour chaque page du rapport.
    
 
Les modules ne peuvent être envoyés qu'au format Texte MS-DOS. Dès lors, si vous spécifiez  **acSendModule** pour l'argument _ObjectType_, vous devez spécifier  **acFormatTXT** pour l'argument _OutputFormat_.
 

 

 **Remarque**  Vous pouvez enregistrer le fichier au format PDF ou XPS à partir d'un programme Microsoft Office System 2007 uniquement après avoir installé un complément. Pour plus d'informations, recherchez « Activer la prise en charge d'autres formats tels que PDF et XPS » sur le site Web d'Office.
 

 **Lien fourni par :**
 
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Luke Chung,[FMS, Inc.](http://www.fmsinc.com/)
 

 

- [Fonctionnalités et limites d'utilisation de la méthode EnvoyerObjet pour envoyer des E-mails](http://www.fmsinc.com/microsoftaccess/email/sendobject.mdl)
    
 
 **Lien fourni par :**
 
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Communauté[UtterAccess](http://www.utteraccess.com)
 

 

- [Courrier électronique à partir d'Access](http://www.utteraccess.com/forum/Email-Access-t130485.mdl)
    
 

## Exemple

L'exemple de code suivant inclut la table Employés au format Microsoft Excel dans un message électronique, en spécifiant le contenu des lignes  **À**,  **Cc** et **Objet** du message. Celui-ci est envoyé immédiatement, sans modification.
 

 

```
DoCmd.SendObject acSendTable, "Employees", acFormatXLS, _ 
    "Nancy Davolio; Andrew Fuller", "Joan Weber", , _ 
    "Current Spreadsheet of Employees", , False
```

L'exemple suivant montre comment créer un message électronique avec Microsoft Outlook, et l'afficher à l'utilisateur.
 

 
 **Exemple de code fourni par :**[Référence du programmeur Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)
 

 



```
Public Function CreateEmailWithOutlook( _
    MessageTo As String, _
    Subject As String, _
    MessageBody As String)

    ' Define app variable and get Outlook using the "New" keyword
    Dim olApp As New Outlook.Application
    Dim olMailItem As Outlook.MailItem  ' An Outlook Mail item
 
    ' Create a new email object
    Set olMailItem = olApp.CreateItem(olMailItem)

    ' Add the To/Subject/Body to the message and display the message
    With olMailItem
        .To = MessageTo
        .Subject = Subject
        .Body = MessageBody
        .Display    ' To show the email message to the user
    End With

    ' Release all object variables
    Set olMailItem = Nothing
    Set olApp = Nothing

End Function
```

L'exemple suivant montre comment créer un message électronique avec Microsoft Outlook, et l'envoyer sans l'afficher à l'utilisateur.
 

 



```
Public Function SendEmailWithOutlook( _
    MessageTo As String, _
    Subject As String, _
    MessageBody As String)

    ' Define app variable and get Outlook using the "New" keyword
    Dim olApp As New Outlook.Application
    Dim olMailItem As Outlook.MailItem  ' An Outlook Mail item
 
    ' Create a new email object
    Set olMailItem = olApp.CreateItem(olMailItem)

    ' Add the To/Subject/Body to the message and display the message
    With olMailItem
        .To = MessageTo
        .Subject = Subject
        .Body = MessageBody
        .Send       ' Send the message immediately
    End With

    ' Release all object variables
    Set olMailItem = Nothing
    Set olApp = Nothing

End Function
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

Luke Chung est le fondateur et le président de FMS, Inc., l'un des principaux fournisseurs de solutions de base de données et d'outils de développement personnalisés.
 

 
Wrox Press est guidé par la philosophie de programmeur à programmeur. Les livres Wrox sont écrits par des programmeurs pour des programmeurs et la marque Wrox est synonyme de solutions de référence aux problèmes de programmation concrets.
 

 
UtterAccess est le premier forum d'aide et wiki de Microsoft Access. Cliquez ici pour le rejoindre.
 

 

## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


 
[Objet DoCmd](3ce44cca-9979-0a1e-9787-079a52ce528f.md)
#### Autres ressources


 
[Membres de l'objet DoCmd](3e7ade9e-86e4-0751-188b-5d31c9101651.md)