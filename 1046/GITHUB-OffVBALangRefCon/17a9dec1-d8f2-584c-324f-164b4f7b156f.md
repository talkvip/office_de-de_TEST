
# Chamando os procedimentos Sub e Function

Para chamar um procedimento  **Sub** de outro[procedimento](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md), digite o nome do procedimento e inclua os valores de qualquer [argumento](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md) necessário. A instrução **Call** não é necessária, mas se você usá-la, será preciso anexar qualquer argumento entre parênteses.

Você pode usar um procedimento  **Sub** para organizar outros procedimentos para que eles possam ser mais facilmente compreendidos e depurados. No exemplo a seguir, o procedimento **Sub** `Main` chamará o procedimento **Sub** `MultiBeep`, passando o valor 56 para seu argumento. Depois que  `MultiBeep` for executado, o controle retornará a `Main` e `Main` chamará o procedimento **Sub** `Message`.  `Message` exibirá uma caixa de mensagem; quando o usuário clicar em **OK**, o controle retornará a `Main` e chamará o procedimento **Sub** `MultiBeep`, passando o valor 56 para seu argumento. Depois que  `MultiBeep` for executado, o controle retornará a `Main` e `Main` chamará o procedimento **Sub** `Message`.  `Message` exibirá uma caixa de mensagem; quando o usuário clicar em **OK**, o controle retornará a `Main` e `Main` terminará.



```
Sub Main() 
 MultiBeep 56 
 Message 
End Sub 
 
Sub MultiBeep(numbeeps) 
 For counter = 1 To numbeeps 
 Beep 
 Next counter 
End Sub 
 
Sub Message() 
 MsgBox "Time to take a break!" 
End Sub 

```


## Chamando os procedimentos Sub com mais de um argumento

O exemplo a seguir mostra duas maneiras de chamar um procedimento  **Sub** com mais de um argumento. Da segunda vez que for chamado, será preciso usar parênteses antes e depois dos argumentos, pois a instrução **Call** será usada.


```
Sub Main() 
 HouseCalc 99800, 43100 
 Call HouseCalc(380950, 49500) 
End Sub 
 
Sub HouseCalc(price As Single, wage As Single) 
 If 2.5 * wage <= 0.8 * price Then 
 MsgBox "You cannot afford this house." 
 Else 
 MsgBox "This house is affordable." 
 End If 
End Sub 

```


## Usando parênteses ao chamar os procedimentos de função

Para usar o valor de retorno de uma função, atribua a função a uma [variável](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md) e anexe os argumentos entre parênteses, como mostrado no exemplo a seguir.


```
Answer3 = MsgBox("Are you happy with your salary?", 4, "Question 3") 

```

Se você não estiver interessado no valor de retorno de uma função, será possível chamar uma função da mesma forma que chama um procedimento  **Sub**. Omita os parênteses, liste os argumentos e não atribua a função a uma variável, como mostrado no exemplo a seguir.




```
MsgBox "Task Completed!", 0, "Task Box" 

```

Se você incluir os parênteses no exemplo, a instrução causará um erro de sintaxe.


## Passando argumentos nomeados

Uma instrução em um procedimento  **Sub** ou **Function** pode passar valores para procedimentos chamados usando[argumentos nomeados](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md). Você pode listar argumentos nomeados em qualquer ordem. Um argumento nomeado consiste no nome do argumento seguido por dois pontos e o sinal de igual ( **:=** ), e o valor atribuído ao argumento.

O exemplo a seguir chama a função  **MsgBox** usando argumentos nomeados sem valor de retorno.




```
MsgBox Title:="Task Box", Prompt:="Task Completed!" 

```

O exemplo a seguir chama a função  **MsgBox** usando argumentos nomeados. O valor de retorno é atribuído à variável.




```
answer3 = MsgBox(Title:="Question 3", _ 
Prompt:="Are you happy with your salary?", Buttons:=4) 

```

