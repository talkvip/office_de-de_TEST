
# Função CreateObject



Cria e retorna uma referência a um [objeto ActiveX](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md).
 **Sintaxe**
 **CreateObject(** _class,[nomedoservidor]_ **)**
A sintaxe da função  **CreateObject** tem estas partes:


|**Parte**|**Descrição**|
|:-----|:-----|
| _classe_|Obrigatório;  **Variant** ( **String** ). O nome e a classe do aplicativo do objeto a ser criado.|
| _nomedoservidor_|Opcional;  **Variant** ( **String** ). O nome do servidor de rede onde o objeto será criado. Se _nomedoservidor_ for uma cadeia de caracteres vazia (""), a máquina local será usada.|
O argumento  _classe_[](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md) usa a sintaxe _nomedoaplicativo_ **.** _tipodeobjeto_ e tem estas partes:


|**Parte**|**Descrição**|
|:-----|:-----|
| _nomedoaplicativo_|Obrigatório;  **Variant** ( **String** ). O nome do aplicativo que fornece o objeto.|
| _tipodeobjeto_|Obrigatório;  **Variant** ( **String** ). O tipo ou a[classe](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md) do objeto a ser criado.|
 **Remarks**
Todos os aplicativos com suporte a Automação oferecem pelo menos um tipo de objeto. Por exemplo, um aplicativo de processamento de texto pode fornecer um objeto  **Application**, um objeto **Document** e um objeto **Toolbar**.
Para criar um objeto ActiveX, atribua o objeto retornado por  **CreateObject** a uma[variável de objeto](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md):



```
' Declare an object variable to hold the object 
' reference. Dim as Object causes late binding. 
Dim ExcelSheet As Object
Set ExcelSheet = CreateObject("Excel.Sheet")
```

Este código inicia o aplicativo que está criando o objeto, neste caso, uma planilha do Microsoft Excel. Assim que o objeto for criado, referencie-o no código usando a variável de objeto definida por você. No exemplo a seguir, acesse [propriedades](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md) e[métodos](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md) do novo objeto usando a variável de objeto, `ExcelSheet`, e outros objetos do Microsoft Excel, incluindo o objeto  `Application` e a coleção `Cells`.



```
' Make Excel visible through the Application object.
ExcelSheet.Application.Visible = True
' Place some text in the first cell of the sheet.
ExcelSheet.Application.Cells(1, 1).Value = "This is column A, row 1"
' Save the sheet to C:\test.xls directory.
ExcelSheet.SaveAs "C:\TEST.XLS"
' Close Excel with the Quit method on the Application object.
ExcelSheet.Application.Quit
' Release the object variable.
Set ExcelSheet = Nothing

```

A declaração de uma variável de objeto com a cláusula  `As Object` cria uma variável que pode conter uma referência a qualquer tipo de objeto. Entretanto, o acesso ao objeto por meio dessa variável é de associação tardia; ou seja, a associação ocorre quando seu programa é executado. Para criar uma variável de objeto que resulte em associação antecipada, ou seja associação quando o programa é compilado, declare a variável de objeto com uma ID da classe especifica. Por exemplo, você pode declarar e criar as seguintes referências do Microsoft Excel:



```
Dim xlApp As Excel.Application 
Dim xlBook As Excel.Workbook
Dim xlSheet As Excel.WorkSheet
Set xlApp = CreateObject("Excel.Application")
Set xlBook = xlApp.Workbooks.Add
Set xlSheet = xlBook.Worksheets(1)

```

A referência por meio de uma variável de associação antecipada pode oferecer desempenho melhor, mas só pode conter uma referência à [classe](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md) especificada na[declaração](b8bdf64f-5920-1ae9-16d0-b26d09524a30.md).
Você pode passar um objeto retornado pela função  **CreateObject** para uma função que esteja esperando um objeto como argumento. Por exemplo, o código a seguir cria e passa uma referência para um objeto Excel.Application:



```
Call MySub (CreateObject("Excel.Application"))
```

Você pode criar um objeto em um computador em uma rede remota passando o nome do computador para o argumento  _nomedoservidor_ de **CreateObject**. Esse nome é igual à parte Nome da Máquina de um nome de compartilhamento: para um compartilhamento chamado "\\MeuServidor\Público", _nomedoservidor_ é "MeuServidor".

 **Observação**  Consulte a documentação do COM (consulte  _Microsoft Developer Network_ ) para obter informações adicionais sobre como tornar um aplicativo visível em um computador em uma rede remota. Pode ser necessário adicionar uma chave do Registro para seu aplicativo.

O código a seguir retorna o número de versão de uma instância do Excel executada em um computador remoto chamado  `MyServer`:



```
Dim xlApp As Object
Set xlApp = CreateObject("Excel.Application", "MyServer")
Debug.Print xlApp.Version
```

Se o servidor remoto não existir ou se estiver indisponível, ocorrerá um erro de tempo de execução.

 **Observação**  Use  **CreateObject** quando não houver uma instância atual do objeto. Se uma instância do objeto já estiver em execução, uma nova instância será iniciada e um objeto do tipo especificado será criado. Para usar a instância atual, ou para iniciar o aplicativo e fazê-lo carregar um arquivo, use a função **GetObject**.

Se um objeto tiver registrado a si mesmo como um objeto de instância única, somente uma instância do objeto será criada, não importa quantas vezes  **CreateObject** seja executado.

## Exemplo

Este exemplo usa a função  **CreateObject** para definir uma referência ( `xlApp`) para o Microsoft Excel. Ela usa a referência para acessar a propriedade  **Visible** do Microsoft Excel e então usa o método **Quit** do Microsoft Excel para fechá-la. Por fim, a própria referência é liberada.


```
Dim xlApp As Object    ' Declare variable to hold the reference.
    
Set xlApp = CreateObject("excel.application")
    ' You may have to set Visible property to True
    ' if you want to see the application.
xlApp.Visible = True
    ' Use xlApp to access Microsoft Excel's 
    ' other objects.

```

