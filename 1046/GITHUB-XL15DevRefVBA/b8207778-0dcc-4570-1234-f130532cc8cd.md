
# Objeto de intervalo (Excel) (em inglês)

Representa uma célula, uma linha, uma coluna, uma seleção de células contendo um ou mais blocos contíguos de células ou um intervalo 3D.


## Exemplo

Use  **Range** ( _arg_ ), em que _arg_ nomeia o intervalo, para retornar um objeto **Intervalo** representando uma única célula ou um intervalo de células. O seguinte exemplo coloca o valor da célula A1 na célula A5.


```
Worksheets("Sheet1").Range("A5").Value = _ 
    Worksheets("Sheet1").Range("A1").Value
```

O seguinte exemplo preenche o intervalo A1:H8 com números aleatórios definindo a fórmula para cada célula no intervalo. Quando ele é usado sem um qualificador de objeto (um objeto à esquerda do período), a propriedade  **Range** retorna um intervalo da planilha ativa. Se a planilha ativa não for uma planilha, o método falhará. Use o método **[Activate](b198dc36-99d0-42db-6cbb-7f68396fd2f5.md)** para ativar uma planilha antes de usar a propriedade **Range** sem um qualificador de objeto explícito.




```
Worksheets("Sheet1").Activate 
Range("A1:H8").Formula = "=Rand()"    'Range is on the active sheet
```

O exemplo a seguir limpa o conteúdo do intervalo chamado  _Criteria_.


 **Observação**  Se você usar um argumento de texto para o endereço do intervalo, terá que especificar o endereço em notação de estilo A1 (ou seja, não poderá usar a notação de estilo L1C1).




```
Worksheets(1).Range("Criteria").ClearContents
```

Use  **Células** ( _linha_, _coluna_ ) em que _linha_ é o índice de linha e _coluna_ é o índice de coluna, para retornar uma única célula. O exemplo a seguir define o valor da célula A1 como 24.




```
Worksheets(1).Cells(1, 1).Value = 24
```

O exemplo a seguir define a fórmula para a célula A2.




```
ActiveSheet.Cells(2, 1).Formula = "=Sum(B1:B5)"
```

Embora você também possa usar  `Range("A1")` para retornar a célula A1, pode haver ocasiões em que a propriedade **Células** é mais prática, pois você pode usar uma variável para a linha ou para a coluna. O seguinte exemplo cria cabeçalhos de coluna e linha na Planilha1. Lembre-se de que após a ativação da planilha, a propriedade **Células** poderá ser usada sem uma declaração explícita de planilha (ela retorna uma célula da planilha ativa).


 **Observação**  Apesar de você poder usar funções de sequência de caracteres do Visual Basic para alterar as referências de estilo A1, é mais fácil (e uma prática de programação muito melhor) usar a notação  `Cells(1, 1)`.




```
Sub SetUpTable() 
Worksheets("Sheet1").Activate 
For TheYear = 1 To 5 
    Cells(1, TheYear + 1).Value = 1990 + TheYear 
Next TheYear 
For TheQuarter = 1 To 4 
    Cells(TheQuarter + 1, 1).Value = "Q" &amp; TheQuarter 
Next TheQuarter 
End Sub
```

Use  _expressão_. **Células** ( _linha_, _coluna_ ), em que _expressão_ é uma expressão que retorna um objeto **Range**, e _linha_ e _coluna_ são relativos ao canto superior esquerdo do intervalo, para retornar parte de um intervalo. O exemplo a seguir define a fórmula para a célula C5.




```
Worksheets(1).Range("C5:C10").Cells(1, 1).Formula = "=Rand()"
```

Use  **Range** ( _célula1, célula2_ ), em que _célula1_ e _célula2_ são objetos **Range** que especificam as células inicial e final, para retornar um objeto **Range**. O exemplo a seguir define o estilo de linha de borda das células A1:J10.


 **Observação**  Lembre-se do ponto na frente de cada ocorrência da propriedade  **Células**. O ponto é necessário se a instrução da declaração anterior **Com** deve ser aplicada à propriedade **Células**. Neste caso, para indicar que as células estão na planilha um (sem o ponto, a propriedade **Células** retornaria as células da planilha ativa).




```
With Worksheets(1) 
    .Range(.Cells(1, 1), _ 
        .Cells(10, 10)).Borders.LineStyle = xlThick 
End With
```

Use  **Deslocamento** ( _linha, coluna_ ), em que _linha_ e _coluna_ são deslocamentos de linha e coluna, para retornar um intervalo em um deslocamento especificado para outro intervalo. O seguinte exemplo seleciona a célula três linhas abaixo e uma coluna à direita da célula no canto superior esquerdo da seleção atual. Você não pode selecionar uma célula que não esteja em uma planilha ativa, portanto, você deve primeiro ativar a planilha.




```
Worksheets("Sheet1").Activate 
  'Can't select unless the sheet is active 
Selection.Offset(3, 1).Range("A1").Select
```

Use  **União** ( _intervalo1, intervalo2_, ...) para retornar intervalos de várias áreas, isto é, intervalos compostos de dois ou mais blocos contíguos de células. O exemplo a seguir cria um objeto definido como a união de intervalos A1:B2 e C3:D4 e, em seguida, seleciona o intervalo definido.




```
Dim r1 As Range, r2 As Range, myMultiAreaRange As Range 
Worksheets("sheet1").Activate 
Set r1 = Range("A1:B2") 
Set r2 = Range("C3:D4") 
Set myMultiAreaRange = Union(r1, r2) 
myMultiAreaRange.Select
```

Se você trabalha com seleções que contêm mais de uma área, a propriedade  **[Áreas](31fc03b4-25b6-27ae-2350-b34c6c6ba255.md)** é útil. Ela divide uma seleção de várias áreas em objetos individuais de **Range** e, em seguida, retorna os objetos como um conjunto. Você pode usar a propriedade **[Contagem](080cbbe7-056f-b21c-9004-171a6acce664.md)** na coleção retornada para verificar uma seleção que contenha mais de uma área, como mostra o exemplo a seguir.




```
Sub NoMultiAreaSelection() 
    NumberOfSelectedAreas = Selection.Areas.Count 
    If NumberOfSelectedAreas > 1 Then 
        MsgBox "You cannot carry out this command " &amp; _ 
            "on multi-area selections" 
    End If 
End Sub
```

 **Código de exemplo fornecido por:** Dennis Wallentin,[VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)

Este exemplo usa o método  **AdvancedFilter** do objeto **Range** para criar uma lista de valores exclusivos e o número de vezes que eles ocorrem no intervalo da coluna A.




```
Sub Create_Unique_List_Count()
    'Excel workbook, the source and target worksheets, and the source and target ranges.
    Dim wbBook As Workbook
    Dim wsSource As Worksheet
    Dim wsTarget As Worksheet
    Dim rnSource As Range
    Dim rnTarget As Range
    Dim rnUnique As Range
    'Variant to hold the unique data
    Dim vaUnique As Variant
    'Number of unique values in the data
    Dim lnCount As Long
    
    'Initialize the Excel objects
    Set wbBook = ThisWorkbook
    With wbBook
        Set wsSource = .Worksheets("Sheet1")
        Set wsTarget = .Worksheets("Sheet2")
    End With
    
    'On the source worksheet, set the range to the data stored in column A
    With wsSource
        Set rnSource = .Range(.Range("A1"), .Range("A100").End(xlDown))
    End With
    
    'On the target worksheet, set the range as column A.
    Set rnTarget = wsTarget.Range("A1")
    
    'Use AdvancedFilter to copy the data from the source to the target,
    'while filtering for duplicate values.
    rnSource.AdvancedFilter Action:=xlFilterCopy, _
                            CopyToRange:=rnTarget, _
                            Unique:=True
                            
    'On the target worksheet, set the unique range on Column A, excluding the first cell
    '(which will contain the "List" header for the column).
    With wsTarget
        Set rnUnique = .Range(.Range("A2"), .Range("A100").End(xlUp))
    End With
    
    'Assign all the values of the Unique range into the Unique variant.
    vaUnique = rnUnique.Value
    
    'Count the number of occurrences of every unique value in the source data,
    'and list it next to its relevant value.
    For lnCount = 1 To UBound(vaUnique)
        rnUnique(lnCount, 1).Offset(0, 1).Value = _
            Application.Evaluate("COUNTIF(" &amp; _
            rnSource.Address(External:=True) &amp; _
            ",""" &amp; rnUnique(lnCount, 1).Text &amp; """)")
    Next lnCount
    
    'Label the column of occurrences with "Occurrences"
    With rnTarget.Offset(0, 1)
        .Value = "Occurrences"
        .Font.Bold = True
    End With

End Sub
```


## Comentários

As seguintes propriedades e métodos para retornar um objeto  **Range** são descritas na seção de exemplos:


- Propriedade  **[Range](9a323305-c822-ef9e-1cc8-ec077a976834.md)**
    
- Propriedade  **[Cells](19c14e41-7d8e-b56f-fd60-717df64edee8.md)**
    
-  **Range** e **Cells**
    
- Propriedade  **[Offset](dfbbd1a2-2f73-fd6a-6277-4584823f55a4.md)**
    
- Método  **[Union](7c70a5be-2696-5fc2-bd69-6c6ff4d3291e.md)**
    

## Métodos



|**Nome**|
|:-----|
|[Activate](a0050055-84e7-7611-a961-887fcb063369.md)|
|[AddComment](89bbacad-4655-bcc1-8010-2ab367cc7b31.md)|
|[AdvancedFilter](fe1a19fc-ab0f-6149-25d9-6102d5789757.md)|
|[AllocateChanges](c751c5fb-ce22-64d1-669c-fdb064cf0408.md)|
|[ApplyNames](3798ecfb-c839-64a9-1088-d7752a3e81ae.md)|
|[ApplyOutlineStyles](eab9b4ed-5d4c-8205-63f2-fa8e4539da73.md)|
|[AutoComplete](723a452f-34e1-fcd1-a2d6-4932c5cc0542.md)|
|[AutoFill](257f6608-9211-86f9-79de-e3c44df8f3fd.md)|
|[AutoFilter](0f773dbf-63e8-f714-d246-f803a74d366c.md)|
|[AutoFit](53a35cd3-00e7-f9f5-2cd2-8492d7814a11.md)|
|[AutoOutline](a2553695-6d45-9b7c-7c45-5255fa3641f0.md)|
|[BorderAround](3ffeb131-45f7-7799-e04a-11577fedaa16.md)|
|[Calculate](7c29afda-4980-6992-fc8d-b4caf2f74660.md)|
|[CalculateRowMajorOrder](8636b550-a3f8-f6cd-baf8-b669354262af.md)|
|[CheckSpelling](22540515-0b0f-ce3c-0145-e46d477d1b5f.md)|
|[Clear](56f46ac7-8bb0-2651-8024-312c7cb7356c.md)|
|[ClearComments](736fd51f-a7cd-02cf-eb45-47e3f3132800.md)|
|[ClearContents](8c957fdd-e99d-ca0e-7d2c-4cb1db62639a.md)|
|[ClearFormats](37777379-857a-f4c7-86aa-b109d5f25757.md)|
|[ClearHyperlinks](1bf0613b-4415-a9cc-88e0-5e71f0ab812b.md)|
|[ClearNotes](24017be9-d3bf-2e8a-4587-d5b0a03fdcaf.md)|
|[ClearOutline](80d82c8d-7670-54b5-7aa5-5c39aadcb990.md)|
|[ColumnDifferences](483995e1-9c8d-c171-4c72-17afd5918d49.md)|
|[Consolidate](d5fb78a3-c3ec-0d1a-c6ad-b33bc90e431c.md)|
|[Copy](ac5207ac-6be5-3c7e-2c61-67954a59e9df.md)|
|[CopyFromRecordset](cec7fded-f4e0-1b1c-5374-8a860828c9cc.md)|
|[CopyPicture](0b187b51-7a52-0db3-9d55-9c1e5bc5e49b.md)|
|[CreateNames](00c7c74f-606d-7eee-ac52-f6b21446f5be.md)|
|[Cut](b9f525c4-c314-450c-f88b-e6c5cdc00112.md)|
|[DataSeries](cfdb0582-8b6c-029d-2a99-4fa1d4b360ea.md)|
|[Delete](7d890cc5-5b5b-35f9-2d97-e4fe48f244ee.md)|
|[DialogBox](d2d4a677-bd6a-910d-ff53-f95585f40925.md)|
|[Dirty](c3f177ef-19b9-07e7-a42f-978874528207.md)|
|[DiscardChanges](adeee827-d680-59f3-0966-2c2ca60a59e1.md)|
|[EditionOptions](5997563b-7f39-6f2d-9265-c72a2d138548.md)|
|[ExportAsFixedFormat](9786c633-e9bd-3ce3-0246-7bcb3c4b4ce1.md)|
|[FillDown](bb7c0b2d-8dd9-13e5-b90a-b2708935afa9.md)|
|[FillLeft](42722b18-8b40-c27b-8bca-ef180cf0f636.md)|
|[FillRight](b0b9a3a5-5f8c-327e-fb41-dec5c1a2f2b3.md)|
|[FillUp](52498f52-95f9-5993-7c44-76cd8b696074.md)|
|[Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)|
|[FindNext](308c6241-2398-13e6-ba68-17ec713376f6.md)|
|[FindPrevious](c03f2e17-d28c-8b0d-b8c8-024863523c99.md)|
|[FlashFill](3ca4a73f-712a-fe69-684d-a959351e5855.md)|
|[FunctionWizard](a9a0c765-4903-4969-8f09-c8f051213a96.md)|
|[Group](da736f64-35df-ecaf-88ac-8c61f7d3c0d0.md)|
|[Insert](e612bbc8-3942-3349-f157-c0459805794a.md)|
|[InsertIndent](1e004333-a64e-55e4-cf8a-d15e47236f94.md)|
|[Justify](f8b4d48b-8cbb-977a-fd44-d354661182d2.md)|
|[ListNames](0523f9b3-d422-76b6-889c-75619cb5b9a6.md)|
|[Merge](eff315d8-fa8f-e452-2bcd-15be4d97a077.md)|
|[NavigateArrow](71e2ce3b-3da8-afd5-7fd3-b922c6f8f1c2.md)|
|[NoteText](cd0e5073-7d04-a52c-f375-f7c59bc8f88a.md)|
|[Parse](3580aeb7-e868-894a-9dd5-8e37475fb267.md)|
|[PasteSpecial](d3e991f2-7ef7-2ebc-d4bc-ba4c26be472e.md)|
|[PrintOut](42d36dbb-5910-530f-5aea-3793a36dc82b.md)|
|[PrintPreview](b429a45c-864f-1c48-0775-1cf240f6e7ac.md)|
|[RemoveDuplicates](0e74bde2-08b3-898d-0b30-53de911bd7e9.md)|
|[RemoveSubtotal](ec1fd131-551d-009f-1eea-033d805bb34d.md)|
|[Replace](12647334-f911-69e4-de31-b4df2722eff3.md)|
|[RowDifferences](89030ca3-9f59-7426-d050-89dcabf00887.md)|
|[Run](b7a0480a-9f10-8aad-6592-3cbde72720cd.md)|
|[Select](46c12f85-fae5-15ea-3500-81ff8be49cdb.md)|
|[SetPhonetic](69a1e491-5505-621a-5ea0-b0600796caa3.md)|
|[Show](c04cbae7-c424-befd-df73-e92bbe9e2e41.md)|
|[ShowDependents](f2e062b2-733b-d0e5-b5ed-9587b104bbc7.md)|
|[ShowErrors](02366ef0-b4dc-a10c-e186-d9392a8b656c.md)|
|[ShowPrecedents](02b8ca94-d251-a6be-1551-1ba769c3c0fa.md)|
|[Sort](ede52b2f-9025-fc83-9c16-f09c6b89c5c2.md)|
|[SortSpecial](706420cb-989a-1b48-b051-ca6e5fe45824.md)|
|[Speak](12928814-9534-c9f0-e351-7d26f77869e0.md)|
|[SpecialCells](30c2035c-34e3-3b1a-f243-69a9fed97f3b.md)|
|[SubscribeTo](6120c474-f4a9-0dce-dae4-a8b39f3d3656.md)|
|[Subtotal](b4b7b640-5a6c-8c94-d9ab-c9a557190829.md)|
|[Table](804b0e1d-e92d-387d-1054-90643bfd16ff.md)|
|[TextToColumns](0b0bf329-ab99-7edc-1b8f-aad03513abde.md)|
|[Ungroup](ac20c780-1a8e-2709-13c4-a6ca8220fb0a.md)|
|[UnMerge](dfc49876-29b0-0b61-fe18-3953438f7452.md)|

## Propriedades



|**Nome**|
|:-----|
|[AddIndent](47cfb2a4-9050-354f-08f6-e86f0164be02.md)|
|[Address](aaa2432e-9bb1-4a48-3868-86455bc53938.md)|
|[AddressLocal](20332d15-dc37-1819-472f-ef208d8b3a5b.md)|
|[AllowEdit](9f03054c-190f-ce3b-54db-bc6e19b7e1c6.md)|
|[Application](10a5b6f8-2ded-be6b-352e-5df9d43c30ed.md)|
|[Areas](31fc03b4-25b6-27ae-2350-b34c6c6ba255.md)|
|[Borders](6d313fed-a8f0-94ba-e239-813685cd1d58.md)|
|[Cells](32a6ecc7-2366-2cec-1feb-0966241a435d.md)|
|[Characters](5011b6d3-23ab-e2a8-9616-c4c73d3ae60e.md)|
|[Column](4f540fae-fc9f-30de-5d71-f6496b78930b.md)|
|[Columns](a1a23288-e911-909d-0bc0-48bdce2ccbac.md)|
|[ColumnWidth](a6364bb1-2e3d-07d6-20e4-c9fa8f7c5ad3.md)|
|[Comment](94c07e38-f232-3fba-f08c-878d3848ac55.md)|
|[Count](080cbbe7-056f-b21c-9004-171a6acce664.md)|
|[CountLarge](3a46ef6d-a339-b15e-990d-b11f462fb602.md)|
|[Creator](d7970f19-b10d-9101-4326-ea2d2460e849.md)|
|[CurrentArray](147f8834-5aef-900f-75de-df91a6a76005.md)|
|[CurrentRegion](39277cc5-07ff-8453-7330-b272b365f9dc.md)|
|[Dependents](47813412-306a-0f99-3ca5-d354b16af468.md)|
|[DirectDependents](266b054e-6838-ffe7-14e2-e712a149e5be.md)|
|[DirectPrecedents](d7eebe51-3e4c-e902-e6a5-1617bd21ef4e.md)|
|[DisplayFormat](c4e044e2-a04e-b655-2973-7e02897ca49d.md)|
|[End](d46d75c9-b152-e93d-82c3-f59f0e7f69da.md)|
|[EntireColumn](7be55670-75fd-fb02-dc1a-9d70e3a9d80d.md)|
|[EntireRow](9e66da51-6cef-4109-ea4e-2acaad42aa1f.md)|
|[Errors](88dcc606-d412-a9ce-82bc-5fbba8baae87.md)|
|[Font](d9cb8667-6c71-d311-a6e5-1d30d5718050.md)|
|[FormatConditions](676ffcc6-f08d-9f91-78af-7b98f8b77dca.md)|
|[Formula](c5be8952-fc3f-bdb3-d4a6-abf9d94eab1e.md)|
|[FormulaArray](a0c8bafb-294c-32ff-0591-1a798aebb4b4.md)|
|[FormulaHidden](b6425c86-7e20-e34e-2d96-eb16075c20b6.md)|
|[FormulaLocal](c69325d9-d35d-c15a-ae49-7bde2b628428.md)|
|[FormulaR1C1](76f41bf6-94e2-2e6a-30e4-012a735a3374.md)|
|[FormulaR1C1Local](be0e3270-43fd-e6c7-1209-11ed3204e563.md)|
|[HasArray](fac17206-8671-6209-9133-d56da6ea2b9c.md)|
|[HasFormula](a18bea77-cee9-ae2d-7e97-90a4205e3b1f.md)|
|[Height](e204a719-d7de-cd18-65b9-c34575bd92e5.md)|
|[Hidden](7e785c38-a8ae-3810-a88a-0bfb7b74e2d6.md)|
|[HorizontalAlignment](6689de5b-60de-07db-d2b4-114f0a343ebc.md)|
|[Hyperlinks](d77f695a-faf2-ce9c-1464-f54b76ee52c9.md)|
|[ID](0ff7f261-8829-2858-5097-a638c01e5f3c.md)|
|[IndentLevel](f4d5af31-904a-27eb-fb2d-e5ae38a7ebb9.md)|
|[Interior](9599b0f7-9f52-627c-51e6-d8be8aeb9bbf.md)|
|[Item](f7d40273-5069-8a9d-14ee-19df225f864c.md)|
|[Left](634fa7b8-3565-6178-f564-3c5d24c16d26.md)|
|[ListHeaderRows](d71a9b28-cd5d-677c-9ce1-f8de2b350e5f.md)|
|[ListObject](bbc404f0-29bd-bb95-2fc8-f826992c4192.md)|
|[LocationInTable](7a86a0fe-cd46-331e-595b-6be168091d0c.md)|
|[Locked](93c5f21d-6429-3287-0992-c810b9a429a8.md)|
|[MDX](6b22b79b-ce44-ce0d-0bb4-e1bf2cd83578.md)|
|[MergeArea](68586bba-fa9c-e0d4-0eae-a08613551a2c.md)|
|[MergeCells](42904357-5e55-1eb0-9b06-83b446fc6275.md)|
|[Name](39d1a326-e123-443c-29c0-453f7b4a8760.md)|
|[Next](10712827-9abd-6b8a-49e5-65e3554fcd87.md)|
|[NumberFormat](351247d2-e4b9-64a0-6dbe-0df535fa701c.md)|
|[NumberFormatLocal](e34e6f52-9279-7961-adfa-4aa84c44937a.md)|
|[Offset](dfbbd1a2-2f73-fd6a-6277-4584823f55a4.md)|
|[Orientation](4f0588b6-2570-fe2f-0cbe-09868b77cff3.md)|
|[OutlineLevel](bdab08a4-3576-4a65-2556-43ed9e9a576e.md)|
|[PageBreak](0bec0bba-c2c3-33cd-b39e-55971177c2c8.md)|
|[Parent](3b4433cc-ce78-b590-31b4-d74f476e104b.md)|
|[Phonetic](9c6d1d83-b215-d60d-f78f-68e521e25368.md)|
|[Phonetics](fdc05b76-b574-63ec-045a-42fdcfae8a9e.md)|
|[PivotCell](976f6393-db3b-d52a-0cbc-88a73bb7c070.md)|
|[PivotField](56003d2d-60cd-abd2-455e-4a4d3616a615.md)|
|[PivotItem](02a41786-074b-ae34-5d2c-407006fe526d.md)|
|[PivotTable](ae3f77dc-5098-d60f-0afc-f4f01dbc33f0.md)|
|[Precedents](3c00cfb4-1c12-668d-a952-89f9b1ef129f.md)|
|[PrefixCharacter](1f7d5fbc-136a-5164-4cec-0054f8bcd0b1.md)|
|[Previous](6ee986eb-9242-63f3-6885-1ad3730f106b.md)|
|[QueryTable](6370d43c-74b5-1bb9-f849-c70006432504.md)|
|[Range](7edbda7c-98d9-143d-7b5e-bcfb7f237818.md)|
|[ReadingOrder](f367af66-21c8-b63f-7a92-3756ee711b18.md)|
|[Resize](05af0539-8aa3-c83c-1972-dfac618929b9.md)|
|[Row](3c8d7351-4fc6-748b-c2a8-de3dab4b964e.md)|
|[RowHeight](103c7209-9a4f-8f9c-7bdc-3013113867a5.md)|
|[Rows](2b0541f1-119d-8535-8418-ff9482353ec1.md)|
|[ServerActions](dffb9535-3b82-c134-82b0-b87d8bc258ec.md)|
|[ShowDetail](1908af55-f61a-2a0f-d828-350e9a680377.md)|
|[ShrinkToFit](fc9aed64-1000-3419-ceb7-a95c15f8a2d0.md)|
|[SoundNote](05d40e33-b07f-5079-29da-8843e9f16820.md)|
|[SparklineGroups](66c6ef19-08a0-91e8-6fef-e827b80d5e62.md)|
|[Style](78c536c9-7fda-3171-2a93-5c4e57bb8207.md)|
|[Summary](f9e18651-20b6-1094-2ee5-7cd23559498e.md)|
|[Text](e38c15b1-5941-0a28-1acf-328bc214a2e0.md)|
|[Top](0d67ac39-9d35-fc2e-56f1-9bd320a4e8ea.md)|
|[UseStandardHeight](59e0be39-25ea-c18d-919d-506d4f041f45.md)|
|[UseStandardWidth](970e3d68-3147-a52f-b831-ae7780c735e0.md)|
|[Validation](d1cad7e6-bbfa-e280-33e7-048733efc0bc.md)|
|[Value](23f28b24-430a-6ea4-4895-0afff8dff218.md)|
|[Value2](0a5d7e6f-2886-5048-66ad-a5078e3465e7.md)|
|[VerticalAlignment](b09a2dcb-b51b-b477-6247-fd5b11a67ccf.md)|
|[Width](75c3aff6-25a0-2f64-2c25-da213b87393b.md)|
|[Worksheet](af38bdde-d523-a4cd-929e-1f67464b2593.md)|
|[WrapText](5e61b704-af16-7bad-5eeb-f163e3035513.md)|
|[XPath](90a353d7-7222-b387-558a-044cb17f09b9.md)|

## Sobre o colaborador
<a name="AboutContributor"> </a>

Dennis Wallentin é autor do VSTO &amp; .NET &amp; Excel, um blog que se concentra em soluções .NET Framework para o Excel e os serviços do Excel. Dennis desenvolve soluções do Excel para mais de 20 anos e também é co-autor de "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)."

