
# Método WorksheetFunction.Match (Excel)

Retorna a posição relativa de um item em uma matriz que corresponda a um valor especificado em uma ordem especificada. Use MATCH em vez de uma das funções de pesquisa quando você precisar da posição de um item em um intervalo, em vez de no próprio item.


## Sintaxe

 _expressão_. **Match**( ** _Arg1_**, ** _Arg2_**, ** _Arg3_** )

 _expressão_ Uma variável que representa um objeto **WorksheetFunction**.


### Parâmetros



|**Nome**|**Obrigatório/Opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _Arg1_|Obrigatório|**Variant**|Lookup_value - o valor usado para localizar o valor desejado em uma tabela.|
| _Arg2_|Obrigatório|**Variant**|Lookup_array - um intervalo contíguo de células que contém valores de pesquisa possíveis. Lookup_array deve ser uma matriz ou uma referência de matriz.|
| _Arg3_|Opcional|**Variant**|Match_type - o número -1, 0 ou 1. Match_type especifica como o Microsoft Excel corresponde a lookup_value com valores em lookup_array.|

### Valor de retorno

Duplo


## Comentários




- Lookup_value é o valor que você deseja corresponder em lookup_array. Por exemplo, quando você procura um número em uma lista telefônica, está usando o nome da pessoa como o valor de pesquisa, mas o número de telefone é o valor desejado.
    
- Lookup_value pode ser um valor (número, texto ou valor lógico) ou uma referência de célula para um número, texto ou valor lógico.
    

- Se match_type for 1, MATCH localizará o maior valor que seja menor ou igual a lookup_value. Lookup_array deverá ser posicionado em ordem crescente:...-2, -1, 0, 1, 2,..., A-Z, FALSE, TRUE.
    
- Se match_type for 0, MATCH localizará o primeiro valor que seja exatamente igual a lookup_value. Lookup_array poderá estar em qualquer ordem.
    
- Se match_type for -1, MATCH localizará o menor valor que seja maior ou igual a lookup_value. Lookup_array deverá ser posicionado em ordem decrescente: TRUE, FALSE, Z-A... 2, 1, 0, -1, -2,... e assim por diante.
    
- Se match_type for omitido, será considerado 1.
    

- MATCH retorna a posição do valor correspondente em lookup_array, e não o próprio valor. Por exemplo, MATCH("b",{"a","b","c"},0) retorna 2, a posição relativa de "b" na matriz {"a","b","c"}.
    
- MATCH não faz distinção entre letras maiúsculas e minúsculas ao fazer a correspondência de valores de texto.
    
- Se MATCH não conseguir encontrar uma correspondência, retornará um valor de erro #N/A.
    
- Se match_type for 0 e se lookup_value for texto, então você poderá usar os caracteres curinga, o ponto de interrogação (?) e o asterisco (*), no lookup_value. Um ponto de interrogação corresponde a qualquer caractere único; um asterisco corresponde a qualquer sequência de caracteres. Se quiser localizar um ponto de interrogação ou um asterisco, digite um til (~) antes do caractere.
    

## Exemplo

 **Exemplo de código fornecido por:** Holy Macro! Books,[Santa Macro! São 2.500 exemplos de VBA do Excel](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)

Para cada valor na primeira coluna da primeira planilha, este exemplo procura um valor correspondente na pasta de trabalho inteira. Se a macro encontrar um valor correspondente, ela definirá o valor original na primeira planilha em negrito.




```
Sub HighlightMatches()
    Application.ScreenUpdating = False
    
    'Declare variables
    Dim var As Variant, iSheet As Integer, iRow As Long, iRowL As Long, bln As Boolean
       
       'Set up the count as the number of filled rows in the first column of Sheet1.
       iRowL = Cells(Rows.Count, 1).End(xlUp).Row
       
       'Cycle through all the cells in that column:
       For iRow = 1 To iRowL
          'For every cell that is not empty, search through the first column in each worksheet in the
          'workbook for a value that matches that cell value.

          If Not IsEmpty(Cells(iRow, 1)) Then
             For iSheet = ActiveSheet.Index + 1 To Worksheets.Count
                bln = False
                var = Application.Match(Cells(iRow, 1).Value, Worksheets(iSheet).Columns(1), 0)
                
                'If you find a matching value, indicate success by setting bln to true and exit the loop;
                'otherwise, continue searching until you reach the end of the workbook.
                If Not IsError(var) Then
                   bln = True
                   Exit For
                End If
             Next iSheet
          End If
          
          'If you do not find a matching value, do not bold the value in the original list;
          'if you do find a value, bold it.
          If bln = False Then
             Cells(iRow, 1).Font.Bold = False
             Else
             Cells(iRow, 1).Font.Bold = True
          End If
       Next iRow
    Application.ScreenUpdating = True
End SubSub HighlightMatches()

Application.ScreenUpdating = False

'Declare variables
Dim var As Variant, iSheet As Integer, iRow As Long, iRowL As Long, bln As Boolean
   
   'Set up the count as the number of filled rows in the first column of Sheet1.
   iRowL = Cells(Rows.Count, 1).End(xlUp).Row
   
   'Cycle through all the cells in that column:
   For iRow = 1 To iRowL
      'For every cell that is not empty, search through all the columns in all the worksheets in the
      'workbook for a value that matches that cell value.
      If Not IsEmpty(Cells(iRow, 1)) Then
         For iSheet = ActiveSheet.Index + 1 To Worksheets.Count
            bln = False
            var = Application.Match(Cells(iRow, 1).Value, Worksheets(iSheet).Columns(1), 0)
            
            'If you find a matching value, indicate success by setting bln to true and exit the loop;
            'otherwise, continue searching until you reach the end of the workbook.
            If Not IsError(var) Then
               bln = True
               Exit For
            End If
         Next iSheet
      End If
      
      'If you do not find a matching value, do not bold the value in the original list;
      'if you do find a value, bold it.
      If bln = False Then
         Cells(iRow, 1).Font.Bold = False
         Else
         Cells(iRow, 1).Font.Bold = True
      End If
   Next iRow
Application.ScreenUpdating = True
End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

Holy Macro! Livros publica livros de entretenimento para as pessoas que usam o Microsoft Office. Veja o catálogo completo em MrExcel.com.


## Ver também
<a name="AboutContributor"> </a>


#### Conceitos


[Objeto WorksheetFunction](7b1d5639-363d-632c-2cf0-2232562646b6.md)
#### Outros recursos


[Membros do objeto WorksheetFunction](6811ca87-4b53-0bff-88c9-30bf7497879a.md)