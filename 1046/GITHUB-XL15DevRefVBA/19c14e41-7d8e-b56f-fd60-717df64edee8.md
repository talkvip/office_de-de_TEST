
# Propriedade Worksheet.Cells (Excel)

Retorna um objeto  **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** que representa todas as células da planilha (e não apenas as células em uso no momento).


## Sintaxe

 _expressão_. **Cells**

 _expressão_ Uma variável que representa um objeto **Worksheet**.


## Comentários

Como a propriedade  **[Item](f7d40273-5069-8a9d-14ee-19df225f864c.md)** é a propriedade padrão para o objeto **Range**, você pode especificar o índice de linha e de coluna imediatamente após a palavra-chave **Cells**. Para saber mais, consulte a propriedade **Item** e os exemplos deste tópico.

A utilização desta propriedade sem um qualificador de objeto retorna um objeto  **Range**, que representa todas as células da planilha ativa.


## Exemplo

Este exemplo define o tamanho da fonte para a célula C5 na Planilha1 como 14 pontos.


```
Worksheets("Sheet1").Cells(5, 3).Font.Size = 14
```

Este exemplo limpa a fórmula na célula um na Planilha1.




```
Worksheets("Sheet1").Cells(1).ClearContents
```

Este exemplo define a fonte e o tamanho da fonte para todas as células na Planilha1 como Arial de 8 pontos




```
With Worksheets("Sheet1").Cells.Font 
    .Name = "Arial" 
    .Size = 8 
End With
```

 **Exemplo de código fornecido por:** Tom Urtis,[Gerenciamento da programação em Atlas](http://www.atlaspm.com/)

Este exemplo alterna uma classificação entre a ordem crescente e decrescente quando você clica duas vezes em qualquer célula do intervalo de dados. Os dados são classificados com base na coluna da célula em que você clicou duas vezes.




```
Option Explicit
Public blnToggle As Boolean

Private Sub Worksheet_BeforeDoubleClick(ByVal Target As Range, Cancel As Boolean)
    Dim LastColumn As Long, keyColumn As Long, LastRow As Long
    Dim SortRange As Range
    LastColumn = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByColumns, SearchDirection:=xlPrevious).Column
    
    keyColumn = Target.Column
    
    If keyColumn <= LastColumn Then
    
        Application.ScreenUpdating = False
        Cancel = True
        LastRow = Cells(Rows.Count, keyColumn).End(xlUp).Row
        Set SortRange = Target.CurrentRegion
        
        blnToggle = Not blnToggle
        If blnToggle = True Then
            SortRange.Sort Key1:=Cells(2, keyColumn), Order1:=xlAscending, Header:=xlYes
        Else
            SortRange.Sort Key1:=Cells(2, keyColumn), Order1:=xlDescending, Header:=xlYes
        End If
    
        Set SortRange = Nothing
        Application.ScreenUpdating = True
        
    End If
End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

MVP Tom Urtis é o fundador da Atlas Programming Management, uma completa do serviço Microsoft Office e o Excel business solutions empresa no Silicon Valley. João tem mais de 25 anos de experiência em gerenciamento de negócios e desenvolvimento Microsoft Office aplicativos e é co-autor de "Holy Macro! It's 2,500 Excel VBA Examples."


## Ver também
<a name="AboutContributor"> </a>


#### Conceitos


[Objeto Worksheet](182b705e-854a-81cc-a4b0-59b942de55ae.md)
#### Outros recursos


[Membros do objeto Worksheet](f8c1afea-1a1c-f5e4-37e3-52c434c8c157.md)