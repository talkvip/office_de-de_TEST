
# Objeto AutoFilter (Excel)

Representa a filtragem automática para a planilha especificada.


 **Observação**  Quando você usar  **AutoFilter** com datas, o formato deverá ser consistente com os separadores de data em inglês ("/") em vez de com as configurações locais ("."). Uma data válida seria "2/2/2007", enquanto "2.2.2007" é inválida.


 **Observação**  Trabalhar com objetos (por exemplo, o objeto  **Interior** ) exige a adição de uma referência a um objeto. Você encontrará mais informações sobre a atribuição de uma referência de Object a uma variável ou a uma propriedade na[Instrução Set](http://msdn.microsoft.com/library/59de2927-b338-0038-50b9-3379d7331935%28Office.15%29.aspx).


## Exemplo

Use a propriedade  **[AutoFilter](766f8501-dae7-32a7-9fae-70a87d0a8eba.md)** para retornar o objeto **AutoFilter**. Use a propriedade **[Filters](4a22dcab-4d06-01a8-7811-4590cf28f506.md)** para retornar uma coleção de filtros de coluna individuais. Use a propriedade **[Range](f8d1aca1-0d69-161a-981a-4dd10826e9d6.md)** para retornar o objeto **Range** que representa todo o intervalo filtrado. O exemplo a seguir armazena o endereço e os critérios de filtragem para a filtragem atual e aplica novos filtros.


```
Dim w As Worksheet 
Dim filterArray() 
Dim currentFiltRange As String 
 
Sub ChangeFilters() 
 
Set w = Worksheets("Crew") 
With w.AutoFilter 
 currentFiltRange = .Range.Address 
 With .Filters 
 ReDim filterArray(1 To .Count, 1 To 3) 
 For f = 1 To .Count 
 With .Item(f) 
 If .On Then 
 filterArray(f, 1) = .Criteria1 
 If .Operator Then 
 filterArray(f, 2) = .Operator 
 filterArray(f, 3) = .Criteria2 
 End If 
 End If 
 End With 
 Next 
 End With 
End With 
 
w.AutoFilterMode = False 
w.Range("A1").AutoFilter field:=1, Criteria1:="S" 
 
End Sub
```

Para criar um objeto  **AutoFilter** para uma planilha, você deve ativar a filtragem automática para um intervalo na planilha manualmente ou usando o método **[AutoFilter](0f773dbf-63e8-f714-d246-f803a74d366c.md)** do objeto **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)**. O exemplo a seguir usa os valores armazenados em variáveis no nível do módulo no exemplo anterior para restaurar a filtragem automática original para a planilha Equipe.




```
Sub RestoreFilters() 
Set w = Worksheets("Crew") 
w.AutoFilterMode = False 
For col = 1 To UBound(filterArray(), 1) 
 If Not IsEmpty(filterArray(col, 1)) Then 
 If filterArray(col, 2) Then 
 w.Range(currentFiltRange).AutoFilter field:=col, _ 
 Criteria1:=filterArray(col, 1), _ 
 Operator:=filterArray(col, 2), _ 
 Criteria2:=filterArray(col, 3) 
 Else 
 w.Range(currentFiltRange).AutoFilter field:=col, _ 
 Criteria1:=filterArray(col, 1) 
 End If 
 End If 
Next 
End Sub 

```


 **Observação**  Quando você usar  **AutoFilter** com datas, o formato deverá ser consistente com os separadores de data em inglês ("/") em vez de com as configurações locais ("."). Uma data válida seria "2/2/2007", enquanto "2.2.2007" é inválida.


## Ver também


#### Outros recursos


[Referência do modelo de objeto do Excel](http://msdn.microsoft.com/library/11ea8598-8a20-92d5-f98b-0da04263bf2c%28Office.15%29.aspx)
[Membros do objeto AutoFilter](7a659664-47a0-8b1b-524e-f808cda97d84.md)