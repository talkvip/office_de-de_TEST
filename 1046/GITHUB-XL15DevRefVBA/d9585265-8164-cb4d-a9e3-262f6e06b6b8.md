
# Método Range.Find (Excel)

Localiza informações específicas em um intervalo.


## Sintaxe

 _expressão_. **Find**( ** _What_**, ** _After_**, ** _LookIn_**, ** _LookAt_**, ** _SearchOrder_**, ** _SearchDirection_**, ** _MatchCase_**, ** _MatchByte_**, ** _SearchFormat_** )

 _expressão_ Uma variável que representa um objeto **Range**.


### Parâmetros



|**Nome**|**Obrigatório/Opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _What_|Obrigatório|**Variant**|Os dados a serem pesquisados. Pode ser uma cadeia de caracteres ou qualquer tipo de dados do Microsoft Excel.|
| _After_|Opcional|**Variant**|A célula em que você deseja que a pesquisa comece. Isso corresponde à posição da célula ativa quando uma pesquisa é concluída da interface do usuário. Observe que  _After_ deve ser uma célula única no intervalo. Lembre-se de que a pesquisa começa após essa célula; a célula especificada não será pesquisada até o método voltar a envolver essa célula. Se você não especificar esse argumento, a pesquisa começará após a célula no canto superior esquerdo do intervalo.|
| _LookIn_|Opcional|**Variant**|O tipo de informação.|
| _LookAt_|Opcional|**Variant**|Pode ser uma das seguintes constantes  **XlLookAt**: **xlWhole** ou **xlPart**.|
| _SearchOrder_|Opcional|**Variant**|Pode ser uma das seguintes constantes  **XlSearchOrder**: **xlByRows** ou **xlByColumns**.|
| _SearchDirection_|Opcional|[XlSearchDirection](fc53fda7-d01c-c407-ccdb-d12004a4dcee.md)|A direção da pesquisa.|
| _MatchCase_|Opcional|**Variant**|**True** para fazer com que a pesquisa passe a diferenciar maiúsculas de minúsculas. O valor padrão é **False**.|
| _MatchByte_|Opcional|**Variant**|Usado somente se você tiver selecionado ou instalado suporte a idiomas de byte duplo.  **True** para fazer caracteres de byte duplo corresponderem somente a caracteres de byte duplo. **False** para fazer caracteres de byte duplo corresponderem a seus equivalentes de byte único.|
| _SearchFormat_|Opcional|**Variant**|O formato da pesquisa.|

### Valor de retorno

Um objeto [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md) que representa a primeira célula onde essas informações são encontradas.


## Comentários

Esse método retornará  **Nothing** se nenhuma correspondência for encontrada. O método **Find** não afeta a seleção ou a célula ativa.

As configurações para  _LookIn_,  _LookAt_,  _SearchOrder_ e _MatchByte_ serão salvas sempre que você usar esse método. Se você não especificar valores para esses argumentos, na próxima vez que você chamar o método, os valores salvos serão usados. A configuração desses argumentos altera as configurações na caixa de diálogo **Localizar** e a alteração das configurações na caixa de diálogo **Localizar** salvará os valores usados caso você omita os argumento. Para evitar problemas, defina esses argumentos de forma explícita sempre que usar este método.

Você pode usar os métodos [FindNext](308c6241-2398-13e6-ba68-17ec713376f6.md) e[FindPrevious](c03f2e17-d28c-8b0d-b8c8-024863523c99.md) para repetir a pesquisa.

Quando a pesquisa atingir o final do intervalo de pesquisa especificado, voltará ao início do intervalo. Para parar uma pesquisa quando isso acontecer, salve o endereço da primeira célula encontrada e então teste cada endereço de célula sucessivo encontrado em relação a esse endereço salvo.

Para localizar as células que correspondam a padrões mais complicados, usem uma instrução  `For Each...Next` com o operador **Like**. Por exemplo, o código a seguir pesquisa todas as células no intervalo A1:C5 que usem uma fonte cujo nome comece com as letras Cour. Quando o Microsoft Excel encontrar uma correspondência, ele alterará a fonte para Times New Roman.

 `For Each c In [A1:C5] If c.Font.Name Like "Cour*" Then c.Font.Name = "Times New Roman" End If Next`




## Exemplo

Este exemplo localiza todas as células no intervalo A1:A500 na planilha um, que contém o valor 2 e o altera para 5.


```
With Worksheets(1).Range("a1:a500") 
    Set c = .Find(2, lookin:=xlValues) 
    If Not c Is Nothing Then 
        firstAddress = c.Address 
        Do 
            c.Value = 5 
            Set c = .FindNext(c) 
        Loop While Not c Is Nothing And c.Address <> firstAddress 
    End If 
End With
```

 **Exemplo de código fornecido por:** Holy Macro! Books,[Santa Macro! São 2.500 exemplos de VBA do Excel](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)

Este exemplo obtém um caminho e o nome de uma pasta de trabalho e um termo de pesquisa, e pesquisa a pasta de trabalho especificada no termo da pesquisa. Se o termo da pesquisa for encontrado, o endereço do resultado será armazenado na célula D10 da pasta de trabalho atual.




```
Option Explicit

Sub FindAddress()
    'Defining the variables.
    Dim GCell As Range
    Dim Page$, Txt$, MyPath$, MyWB$, MySheet$
    
    
    'The text for which to search.
    Txt = "Hello"
    'The path to the workbook in which to search.
    MyPath = "C:\Your\File\Path\"
    'The name of the workbook in which to search.
    MyWB = "YourFileName.xls"
    
    'Use the current sheet as the place to store the data for which to search.
    MySheet = ActiveSheet.Name
    
    'If an error occurs, use the error handling routine at the end of this file.
    On Error GoTo ErrorHandler
    
    'Turn off screen updating, and then open the target workbook.
    Application.ScreenUpdating = False
    Workbooks.Open FileName:=MyPath &amp; MyWB
    
    'Search for the specified text
    Set GCell = ActiveSheet.Cells.Find(Txt)
    
    'Record the address of the data, along with the date, in the current workbook.
    With ThisWorkbook.ActiveSheet.Range("D10")
        .Value = "Address of " &amp; Txt &amp; ":"
        .Offset(0, 1).Value = "Date:"
        .Offset(1, 0).Value = GCell.Address
        .Offset(1, 1).Value = Date
        .Columns.AutoFit
        .Offset(1, 1).Columns.AutoFit
    End With
    
    'Close the data workbook, without saving any changes, and turn screen updating back on.
    ActiveWorkbook.Close savechanges:=False
    Application.ScreenUpdating = True
Exit Sub

'Error Handling section.
ErrorHandler:
Select Case Err.Number
        'Common error #1: file path or workbook name is wrong.
        Case 1004
            Range("D10:E11").ClearContents
            Application.ScreenUpdating = True
            MsgBox "The workbook " &amp; MyWB &amp; " could not be found in the path" &amp; vbCrLf &amp; MyPath &amp; "."
        Exit Sub
        
        'Common error #2: the specified text wasn't in the target workbook.
        Case 9, 91
            ThisWorkbook.Sheets(MySheet).Range("D10:E11").ClearContents
            Workbooks(MyWB).Close False
            Application.ScreenUpdating = True
            MsgBox "The value " &amp; Txt &amp; " was not found."
        Exit Sub
        
        'General case: turn screenupdating back on, and exit.
        Case Else
            Application.ScreenUpdating = True
        Exit Sub
End Select

End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

Holy Macro! Livros publica livros de entretenimento para as pessoas que usam o Microsoft Office. Veja o catálogo completo em MrExcel.com.


## Ver também
<a name="AboutContributor"> </a>


#### Conceitos


[Objeto Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)
#### Outros recursos


[Membros do objeto Range](4336bf81-1e63-7e44-1792-baf366a027a7.md)