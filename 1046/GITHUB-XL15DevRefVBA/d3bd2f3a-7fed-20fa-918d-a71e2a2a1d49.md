
# Método Application.InputBox (Excel)

Exibe uma caixa de diálogo para entrada de usuário. Retorna as informações inseridas na caixa de diálogo.


## Sintaxe

 _expressão_. **InputBox**( ** _Prompt_**, ** _Title_**, ** _Default_**, ** _Left_**, ** _Top_**, ** _HelpFile_**, ** _HelpContextID_**, ** _Type_** )

 _expressão_ Uma variável que representa um objeto **Application**.


### Parâmetros



|**Nome**|**Obrigatório/Opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _Prompt_|Obrigatório|**String**|A mensagem a ser exibida na caixa de diálogo. Pode ser uma cadeia de caracteres, um número, uma data ou um valor Booliano (o Microsoft Excel força automaticamente o valor como uma  **String** antes de ser exibido).|
| _Title_|Opcional|**Variant**|O título para a caixa de entrada. Se esse argumento for omitido, o título padrão será "Entrada".|
| _Default_|Opcional|**Variant**|Especifica um valor que aparecerá na caixa de texto quando a caixa de diálogo é exibida inicialmente. Se esse argumento for omitido, a caixa de texto será deixada vazia. Esse valor pode ser um objeto  **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)**.|
| _Left_|Opcional|**Variant**|Especifica uma posição x para a caixa de diálogo em relação ao contato superior esquerdo da tela, em pontos.|
| _Top_|Opcional|**Variant**|Especifica uma posição y para a caixa de diálogo em relação ao conto superior esquerdo da tela, em pontos.|
| _HelpFile_|Opcional|**Variant**|O nome do arquivo da Ajuda para esta caixa de entrada. Se os argumentos  _HelpFile_ e _HelpContextID_ estiverem presentes, um botão Ajuda aparecerá na caixa de diálogo.|
| _HelpContextID_|Opcional|**Variant**|O número da ID de contexto do tópico da Ajuda em  _HelpFile_.|
| _Type_|Opcional|**Variant**|Especifica o tipo de dados de retorno. Se esse argumento for omitido, a caixa de diálogo retornará texto.|

### Valor de retorno

Variante


## Comentários

A tabela a seguir lista os valores que podem ser passados no argumento Tipo. Pode ser um ou uma soma de valores. Por exemplo, para uma caixa de entrada que possa aceitar texto e números, defina  _Type_ como 1 + 2.



|**Valor**|**Significado**|
|:-----|:-----|
|0|Uma fórmula|
|1|Um número|
|2|Texto (uma cadeia de caracteres)|
|4|Um valor lógico ( **True** ou **False** )|
|8|Uma referência de célula, como um objeto  **Range**|
|16|Um valor de erro, como #N/A|
|64|Uma matriz de valores|
Use  **InputBox** para exibir uma caixa de diálogo simples de forma que você possa inserir informações a serem usadas em uma macro. A caixa de diálogo tem um botão **OK** e um botão **Cancelar**. Se você escolher o botão **OK**, **InputBox** retornará o valor inserido na caixa de diálogo. Se você clicar no botão **Cancelar**, **InputBox** retornará **False**.

Se  _Type_ for 0, **InputBox** retornará a fórmula no formato de texto ? por exemplo, "=2*PI()/360". Se houver qualquer referência na fórmula, ela será retornada como uma referência no estilo A1 (use **[ConvertFormula](6ed0a76c-9db5-f6ab-a91d-d4e1b6674c53.md)** para converter entre estilos de referência).

Se  _Type_ for 8, **InputBox** retornará um objeto **Range**. Você deve usar a instrução **Set** para atribuir o resultado a um objeto **Range**, como mostrado no exemplo a seguir.




```
Set myRange = Application.InputBox(prompt := "Sample", type := 8)
```

Se você não usar a instrução  **Set**, a variável será definida como o valor no intervalo, em vez do próprio objeto **Range**.

Se você usar o método  **InputBox** para solicitar uma fórmula ao usuário, deverá usar a propriedade **[FormulaLocal](c69325d9-d35d-c15a-ae49-7bde2b628428.md)** para atribuir a fórmula como um objeto **Range**. A fórmula de entrada estará no idioma do usuário.

O método  **InputBox** difere da função **InputBox** pois permite a validação seletiva da entrada do usuário e pode ser usado com os objetos, os valores de erro e as fórmulas do Microsoft Excel. Observe que `Application.InputBox` chama o método **InputBox**; `InputBox` sem um qualificador de objeto chama a função **InputBox**.


## Exemplo

Este exemplo solicita um número do usuário.


```
myNum = Application.InputBox("Enter a number")
```

Este exemplo solicita que o usuário selecione uma célula na Planilha1. O exemplo usa o argumento  _Type_ para garantir que o valor de retorno seja uma referência válida de célula (um objeto **Range** ).




```
Worksheets("Sheet1").Activate 
Set myCell = Application.InputBox( _ 
    prompt:="Select a cell", Type:=8)
```

 **Exemplo de código fornecido por:** Holy Macro! Books,[Santa Macro! São 2.500 exemplos de VBA do Excel](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)

Este exemplo usa uma InputBox para que o usuário selecione um intervalo a ser passado para a função definida pelo usuário ?MyFunction?, que multiplica três valores em um intervalo e retorna o resultado.




```
Sub Cbm_Value_Select()
   'Set up the variables.
   Dim rng As Range
   
   'Use the InputBox dialog to set the range for MyFunction, with some simple error handling.
   Set rng = Application.InputBox("Range:", Type:=8)
   If rng.Cells.Count <> 3 Then
     MsgBox "Length, width and height are needed -" &amp; _
         vbLf &amp; "please select three cells!"
      Exit Sub
   End If
   
   'Call MyFunction by value using the active cell.
   ActiveCell.Value = MyFunction(rng)
End Sub


Function MyFunction(rng As Range) As Double
   MyFunction = rng(1) * rng(2) * rng(3)
End Function
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

Holy Macro! Livros publica livros de entretenimento para as pessoas que usam o Microsoft Office. Veja o catálogo completo em MrExcel.com.


## Ver também
<a name="AboutContributor"> </a>


#### Conceitos


[Objeto Application](19b73597-5cf9-4f56-8227-b5211f657f6f.md)
#### Outros recursos


[Membros do objeto Application](4cb9ca42-8d07-cc9c-2d80-4eb9a5921e1e.md)