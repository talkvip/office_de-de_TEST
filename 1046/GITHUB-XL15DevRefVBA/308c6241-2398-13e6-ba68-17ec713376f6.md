
# Método Range.FindNext (Excel)

Continua uma pesquisa que começou com o método  **[Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)**. Localiza a próxima célula que corresponde às mesmas condições e retorna um objeto **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** que representa a célula. Isso não afeta a seleção ou a célula ativa.


## Sintaxe

 _expressão_. **FindNext**( ** _After_** )

 _expressão_ Uma variável que representa um objeto **Range**.


### Parâmetros



|**Nome**|**Obrigatório/Opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _After_|Opcional|**Variant**|A célula depois da qual você deseja pesquisar. Isso corresponde à posição da célula ativa quando uma pesquisa é concluída a partir da interface do usuário. Esteja ciente de que  _After_ deve ser uma única célula no intervalo. Lembre-se de que a pesquisa começa nessa célula; a célula especificada não será pesquisada até o método envolver novamente essa célula. Se esse argumento não for especificado, a pesquisa começará depois da célula no canto superior esquerdo do intervalo.|

### Valor de retorno

Intervalo


## Comentários

Quando a pesquisa atingir o final do intervalo de pesquisa especificado, voltará ao início do intervalo. Para parar uma pesquisa quando isso acontecer, salve o endereço da primeira célula encontrada e então teste cada endereço de célula sucessivo encontrado em relação a esse endereço salvo.


## Exemplo

Este exemplo localiza todas as células no intervalo A1:A500 com o valor 2 e altera seus valores para 5.


```
With Worksheets(1).Range("a1:a500") 
    Set c = .Find(2, lookin:=xlValues) 
    If Not c Is Nothing Then 
        firstAddress = c.Address 
        Do 
            c.Value = 5 
            Set c = .FindNext(c) 
        Loop While Not c Is Nothing And c.Address <> firstAddress 
    End If 
End With
```

 **Exemplo de código fornecido por:** Dennis Wallentin,[VSTO e .NET e Excel](http://xldennis.wordpress.com/)

Este exemplo localiza todas as células nas quatro primeiras colunas com uma constante ?X? nelas e oculta a coluna com o X.




```
Sub Hide_Columns()

    'Excel objects.
    Dim m_wbBook As Workbook
    Dim m_wsSheet As Worksheet
    Dim m_rnCheck As Range
    Dim m_rnFind As Range
    Dim m_stAddress As String

    'Initialize the Excel objects.
    Set m_wbBook = ThisWorkbook
    Set m_wsSheet = m_wbBook.Worksheets("Sheet1")
    
    'Search the four columns for any constants.
    Set m_rnCheck = m_wsSheet.Range("A1:D1").SpecialCells(xlCellTypeConstants)
    
    'Retrieve all columns that contain an X. If there is at least one, begin the DO/WHILE loop.
    With m_rnCheck
        Set m_rnFind = .Find(What:="X")
        If Not m_rnFind Is Nothing Then
            m_stAddress = m_rnFind.Address
             
            'Hide the column, and then find the next X.
            Do
                m_rnFind.EntireColumn.Hidden = True
                Set m_rnFind = .FindNext(m_rnFind)
            Loop While Not m_rnFind Is Nothing And m_rnFind.Address <> m_stAddress
        End If
    End With

End Sub
```

Este exemplo localiza todas as células nas quatro primeiras colunas com uma constante ?X? nelas e reexibe a coluna com o X.




```
Sub Unhide_Columns()
    'Excel objects.
    Dim m_wbBook As Workbook
    Dim m_wsSheet As Worksheet
    Dim m_rnCheck As Range
    Dim m_rnFind As Range
    Dim m_stAddress As String
    
    'Initialize the Excel objects.
    Set m_wbBook = ThisWorkbook
    Set m_wsSheet = m_wbBook.Worksheets("Sheet1")
    
    'Search the four columns for any constants.
    Set m_rnCheck = m_wsSheet.Range("A1:D1").SpecialCells(xlCellTypeConstants)
    
    'Retrieve all columns that contain X. If there is at least one, begin the DO/WHILE loop.
    With m_rnCheck
        Set m_rnFind = .Find(What:="X", LookIn:=xlFormulas)
        If Not m_rnFind Is Nothing Then
            m_stAddress = m_rnFind.Address
            
            'Unhide the column, and then find the next X.
            Do
                m_rnFind.EntireColumn.Hidden = False
                Set m_rnFind = .FindNext(m_rnFind)
            Loop While Not m_rnFind Is Nothing And m_rnFind.Address <> m_stAddress
        End If
    End With

End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

Dennis Wallentin é autor do VSTO &amp; .NET &amp; Excel, um blog que se concentra em soluções .NET Framework para o Excel e os serviços do Excel. Dennis desenvolve soluções do Excel para mais de 20 anos e também é co-autor de "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)."


## Ver também
<a name="AboutContributor"> </a>


#### Conceitos


[Objeto Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)
#### Outros recursos


[Membros do objeto Range](4336bf81-1e63-7e44-1792-baf366a027a7.md)