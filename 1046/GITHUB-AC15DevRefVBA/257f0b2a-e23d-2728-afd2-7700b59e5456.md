
# Método Application.DCount (Access)

Você pode usar a função  **DCount** para determinar o número de registros em um conjunto de registros especificado (um domínio).


## Sintaxe

 _expressão_. **DCount**( ** _Expressão_**, ** _Domínio_**, ** _Critérios_** )

 _expressão_ Uma variável que representa um objeto **Application**.


### Parâmetros



|**Nome**|**Obrigatório/Opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _Expr_|Obrigatório|**String**|Uma expressão que identifica o campo para o qual você deseja contar registros. Pode ser uma expressão de cadeia de caracteres que identifica um campo em uma tabela ou consulta ou pode ser uma expressão que executa um cálculo com os dados naquele campo. Em  _expr_, você pode incluir o nome de um campo de uma tabela, um controle de um formulário, uma constante ou uma função. Se  _expr_ incluir uma função, ela poderá ser interna ou definida pelo usuário, mas nenhuma outra função de agregação de domínio ou de agregação SQL.|
| _Domain_|Obrigatório|**String**|Uma expressão de cadeia de caracteres que identifica o conjunto de registros que constitui o domínio. Pode ser um nome de tabela ou um nome de uma consulta que não exija um parâmetro.|
| _Criteria_|Opcional|**Variant**|Uma expressão de cadeia de caracteres opcional usada para restringir o intervalo de dados no qual a função  **DCount** é executada. Por exemplo, _criteria_ com frequência é equivalente à cláusula ONDE em uma expressão SQL, sem a palavra ONDE. Se _criteria_ for omitido, a função **DCount** avalia _expr_ em relação ao domínio inteiro. Todos os campos incluídos em _criteria_ também deverão ser campos em _domain_; caso contrário, a função  **DCount** retornará **Null**.|

### Valor de retorno

Variante


## Comentários

Por exemplo, você poderia usar a função  **DCount** em um módulo para retornar o número de registros em uma tabela Pedidos que correspondem a pedidos feitos em uma data em particular.

Use a função  **DCount** para contar o número de registros em um domínio quando você não precisar saber quais são os valores particulares deles. Embora o argumento _expr_ possa executar um cálculo em um campo, a função **DCount** simplesmente conta o número de registros. O valor de qualquer cálculo executado por _expr_ está indisponível.

Use a função  **DCount** em um controle calculado quando for necessário especificar critérios para restringir o intervalo de dados no qual a função é executada. Por exemplo, para exibir o número de pedidos a serem enviados para a Califórnia, defina a propriedade **ControlSource** de uma caixa de texto para a seguinte expressão:




```
=DCount("[OrderID]", "Orders", "[ShipRegion] = 'CA'")
```

Se você simplesmente quiser contar todos os registros em  _domain_ sem especificar qualquer restrição, use a função **Count**.

A função  **Count** foi otimizada para acelerar a contagem de registros em consultas. Use a função **Count** em uma expressão de consulta em vez da função **DCount** e defina critérios opcionais para impor as restrições aos resultados. Use a função **DCount** quando for necessário contar registros em um domínio de dentro de um módulo ou de uma macro de código, ou em um controle calculado.

Você pode usar a função  **DCount** para contar o número de registros com um campo em particular que não esteja na fonte do registro no qual seu formulário ou relatório se baseia. Por exemplo, você poderia exibir o número de pedidos na tabela Pedidos em um controle calculado em um formulário baseado na tabela Produtos.

A função  **DCount** não conta registros com valores **Null** no campo referenciado por _expr_, a menos que  _expr_ seja o caractere curinga asterisco (*). Se você usar um asterisco, a função **DCount** calculará o número total de registros, incluindo os que contêm campos **Null**. O exemplo a seguir calcula o número de registros em uma tabela Pedidos.




```
intX = DCount("*", "Orders")
```

Se  _domain_ for uma tabela com uma chave primária, você também poderá contar o número total de registros ao definir _expr_ como o campo de chave primária, já que nunca haverá um **Null** no campo de chave primária.

Se  _expr_ identificar vários campos, separe os nomes do campo usando um operador de concatenação, um E comercial (&amp;) ou o operador de adição (+). Se você usar um E comercial para separar os campos, a função **DCount** retornará o número de registros com os dados em todos os campos listados. Se você usar o operador de adição, a função **DCount** retornará somente o número de registros com dados em todos os campos listados. O exemplo a seguir demonstrará os efeitos de cada operador quando usado com um campo com dados em todos os registros (ShipName) e um campo que não contenha dados (ShipRegion).




```
intW = DCount("[ShipName]", "Orders") 
intX = DCount("[ShipRegion]", "Orders") 
intY = DCount("[ShipName] + [ShipRegion]", "Orders") 
intZ = DCount("[ShipName] &amp; [ShipRegion]", "Orders")
```


 **Observação**  O e comercial é o operador preferencial para a execução da concatenação de cadeias de caracteres. Evite o uso do operador de adição para tudo o que não for adição numérica, a menos que você queira especificamente propagar  **Nulls** por meio de uma expressão.


## Exemplo

A função a seguir retorna o número de pedidos enviados para um país ou região especificado após uma data de remessa especificada. O domínio é uma tabela Pedidos.


```
Public Function OrdersCount(ByVal strCountry As String, _ 
                            ByVal dteShipDate As Date) As Integer 
 
    OrdersCount = DCount("[ShippedDate]", "Orders", _ 
                  "[ShipCountry] = '" &amp; strCountry &amp; _ 
                  "' AND [ShippedDate] > #" &amp; dteShipDate &amp; "#") 
End Function 

```



Os exemplos a seguir mostram como usar diversos tipos de critérios com a função  **DCount**.

 **Código de exemplo fornecido por:**
![Ícone de Membro da Comunidade](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) A comunidade[UtterAccess](http://www.utteraccess.com)




```
    ' ***************************
    ' Typical Use
    ' Numerical values. Replace "number" with the number to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria] = number")

    ' Strings.
    ' Numerical values. Replace "string" with the string to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria]= 'string'")

    ' Dates. Replace "date" with the string to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria]= #date#")
    ' ***************************

    ' ***************************
    ' Referring to a control on a form
    ' Numerical values
    variable = DCount("[FieldName]", "TableName", "[Criteria] = " &amp; Forms!FormName!ControlName)

    ' Strings
    variable = DCount("[FieldName]", "TableName", "[Criteria] = '" &amp; Forms!FormName!ControlName &amp; "'")

    ' Dates
    variable = DCount("[FieldName]", "TableName", "[Criteria] = #" &amp; Forms!FormName!ControlName &amp; "#")
    ' ***************************

    ' ***************************
    ' Combinations
    ' Multiple types of criteria
    variable = DCount("[FieldName]", "TableName", "[Criteria1] = " &amp; Forms![FormName]![Control1] _
             &amp; " AND [Criteria2] = '" &amp; Forms![FormName]![Control2] &amp; "'" _
            &amp; " AND [Criteria3] =#" &amp; Forms![FormName]![Control3] &amp; "#")
    
    ' Use two fields from a single record.
    variable = DCount("[LastName] &amp; ', ' &amp; [FirstName]", "tblPeople", "[PrimaryKey] = 7")
            
    ' Expressions
    variable = DCount("[Field1] + [Field2]", "tableName", "[PrimaryKey] = 7")
    
    ' Control Structures
    variable = DCount("IIf([LastName] Like 'Smith', 'True', 'False')", "tableName", "[PrimaryKey] = 7")
    ' ***************************
```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

UtterAccess é o fórum principal de wiki e a Ajuda do Microsoft Access. clique aqui para ingressar.


## Ver também
<a name="AboutContributors"> </a>


#### Conceitos


[Objeto Application](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Outros recursos


[Membros do objeto Application](3ab5276c-d52a-72a9-244c-ec92ead48811.md)