
# Evento Form.BeforeUpdate (Access)

O evento  **BeforeUpdate** ocorre antes de os dados alterados em um controle ou registro serem atualizados.


## Sintaxe

 _expressão_. **BeforeUpdate**( ** _Cancel_** )

 _expressão_ Uma variável que representa um objeto **Form**.


### Parâmetros



|**Nome**|**Obrigatório/opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _Cancel_|Obrigatório|**Integer**|A configuração determina se o evento  **BeforeUpdate** ocorrerá. A configuração do argumento _Cancel_ como **True** (?1) cancela o evento **BeforeUpdate**.|

## Comentários

A alteração de dados em um controle usando o Visual Basic ou uma macro que contém a ação  **SetValue** não disparará esses eventos para o controle. No entanto, caso você vá para outro registro ou salve o registro, ocorrerá o evento **BeforeUpdate** do formulário.

Para executar uma macro ou um procedimento de evento quando esse evento ocorrer, defina a propriedade  **BeforeUpdate** com o nome da macro ou como [Procedimento do Evento].

Esse evento não se aplica a botões de opção, caixas de seleção ou botões de alternância em um grupo de opções. Ele se aplica somente ao grupo de opções propriamente dito.

O evento  **BeforeUpdate** é disparado quando um controle ou registro é atualizado. Em um registro, os dados alterados em cada controle são atualizados quando ele perde o foco ou quando o usuário pressiona **ENTER** ou **TAB**. Quando o foco deixa o registro ou se o usuário clicar em  **Salvar Registro** no menu **Registros**, o registro inteiro será atualizado e os dados serão salvos no banco de dados.

Quando você insere dados novos ou alterados em um controle em um formulário e depois vai para outro registro ou salva o registro clicando em  **Save Record** no menu **Records**, o evento  **AfterUpdate** do formulário ocorre imediatamente após o evento **AfterUpdate** do controle. Quando você vai para um registro diferente, os eventos **Exit** e **LostFocus** do controle ocorrem, seguidos do evento **Current** referente ao registro que foi movido, e os eventos **Enter** e **GotFocus** do primeiro controle nesse registro. Para executar a macro ou o procedimento de evento **AfterUpdate** sem executar as macros ou os procedimentos de evento **Exit** e **LostFocus**, salve o registro usando o comando **Save Record** no menu **Records**.

As macros e os procedimentos de evento de  **BeforeUpdate** só serão executados se você alterar os dados em um controle. Esse evento não ocorre quando um valor é alterado em um controle calculado. As macros e os procedimentos de evento de **BeforeUpdate** de um formulário só serão executados se os dados forem alterados em um ou mais controles do registro.

Para formulários, você pode usar o evento  **BeforeUpdate** para cancelar a atualização de um registro antes de ir para outro registro.

Mesmo que o usuário insira um novo valor no controle, a configuração da propriedade  **OldValue** não será alterada até que os dados sejam salvos (e o registro seja atualizado). Se você cancelar uma atualização, o valor da propriedade **OldValue** substituirá o valor existente no controle.

O evento  **BeforeUpdate** é usado muitas vezes para validar os dados, especialmente quando executados em validações complexas, como as que:


- Envolvem condições para mais de um valor em um formulário.
    
- Exibem mensagens de erro distintas à medida que dados diferentes são inseridos.
    
- Podem ser substituídas pelo usuário.
    
- Contêm referências a controles de outros formulários ou contêm funções definidas pelo usuário.
    

 **Observação**  Para realizar validações simples ou mais complexas, como solicitar um valor em um campo ou validar mais de um controle em um formulário, você pode usar a propriedade  **ValidationRule** para controles e as propriedades **ValidationRule** e **Required** para campos e registros em tabelas.

Ocorrerá um erro em tempo de execução se você tentar modificar os dados contidos no controle que disparou o evento  **BeforeUpdate** no procedimento de evento.


## Exemplo

O exemplo a seguir mostra como é possível usar um procedimento de evento  **BeforeUpdate** para verificar se o nome de um produto já foi inserido no banco de dados. Depois que o usuário digitar o nome de um produto na caixa **ProductName**, o valor será comparado com o campo  **ProductName** da tabela **Products**. Se houver um valor correspondente na tabela  **Products**, será exibida uma mensagem informando ao usuário que o produto já foi inserido.

Para testar o exemplo, adicione o seguinte procedimento de evento a um formulário denominado  **Products** que contém uma caixa de texto denominada **ProductName**.




```
Private Sub ProductName_BeforeUpdate(Cancel As Integer) 
    If(Not IsNull(DLookup("[ProductName]", _ 
        "Products", "[ProductName] ='" _ 
        &amp; Me!ProductName &amp; "'"))) Then 
        MsgBox "Product has already been entered in the database." 
        Cancel = True 
        Me!ProductName.Undo 
    End If 
End Sub
```



O exemplo a seguir mostra como exibir uma mensagem quando um controle estiver vazio em um formulário contínuo, quando um usuário vai para outro registro.

 **Código de exemplo fornecido por:** Bill Jelen,[MrExcel.com](http://www.mrexcel.com/)




```
Private Sub Form_BeforeUpdate(Cancel As Integer)
    Dim oContr As Control
    For Each oContr In Me.Detail.Controls
        If IsNull(oContr) = True Then
            If MsgBox(oContr.Name &amp; " is empty", vbOKCancel) = vbCancel Then
                Cancel = True: oContr.SetFocus: Exit Sub
            End If
        End If
    Next oContr
End Sub
```

O exemplo a seguir mostra como usar o evento  **BeforeUpdate** de um formulário para exigir que um valor seja inserido em um controle quando outro controle também tenha dados.

 **Código de exemplo fornecido por:** A[Referência do programador do Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)




```
Private Sub Form_BeforeUpdate(Cancel As Integer)
If (IsNull(Me.FieldOne)) Or (Me.FieldOne.Value =  "") Then
    ' No action required
Else
    If (IsNull(Me.FieldTwo)) or (Me.FieldTwo.Value = "") Then
        MsgBox "You must provide data for field 'FieldTwo', " &amp; _
            "if a value is entered in FieldOne", _
            vbOKOnly, "Required Field"
        Me.FieldTwo.SetFocus
        Cancel = True
        Exit Sub
    End If
End If

End Sub

```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

Holy Macro! Livros publica livros de entretenimento para as pessoas que usam o Microsoft Office. Veja o catálogo completo em MrExcel.com.

Wrox Press é impulsionada pelo programador para programador filosofia. Livros Wrox são escritos por programadores para programadores, ea marca Wrox significa soluções autoritárias para problemas de programação do mundo real.


## Ver também
<a name="AboutContributors"> </a>


#### Conceitos


[Objeto Form](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Outros recursos


[Membros do objeto Form](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)