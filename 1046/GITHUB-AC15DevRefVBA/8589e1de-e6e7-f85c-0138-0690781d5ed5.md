
# Propriedade Application.FileDialog (Access)

Retorna um objeto  **FileDialog** que representa uma única instância de uma caixa de diálogo de arquivo. Somente leitura.


## Sintaxe

 _expressão_. **FileDialog**( ** _dialogType_** )

 _expressão_ Uma variável que representa um objeto **Application**.


### Parâmetros



|**Nome**|**Obrigatório/Opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _dialogType_|Obrigatório|**MsoFileDialogType**|Uma constante  **[MsoFileDialogType](http://msdn.microsoft.com/library/ee445a67-1193-f446-4bd2-963c07fba5ae%28Office.15%29.aspx)** que representa o tipo de caixa de diálogo.|

## Comentários

As constantes  **msoFileDialogOpen** e **msoFileDialogSaveAs** não têm suporte no Microsoft Access.


## Exemplo

Este exemplo ilustra como usar o objeto FileDialog para exibir uma caixa de diálogo que permite que o usuário selecione um ou mais arquivos. Os arquivos selecionados serão, então, adicionados a uma caixa de listagem chamada FileList.


```
Private Sub cmdFileDialog_Click() 
  
   ' Requires reference to Microsoft Office 11.0 Object Library. 
 
   Dim fDialog As Office.FileDialog 
   Dim varFile As Variant 
 
   ' Clear listbox contents. 
   Me.FileList.RowSource = "" 
 
   ' Set up the File Dialog. 
   Set fDialog = Application.FileDialog(msoFileDialogFilePicker) 
 
   With fDialog 
 
      ' Allow user to make multiple selections in dialog box 
      .AllowMultiSelect = True 
             
      ' Set the title of the dialog box. 
      .Title = "Please select one or more files" 
 
      ' Clear out the current filters, and add our own. 
      .Filters.Clear 
      .Filters.Add "Access Databases", "*.MDB" 
      .Filters.Add "Access Projects", "*.ADP" 
      .Filters.Add "All Files", "*.*" 
 
      ' Show the dialog box. If the .Show method returns True, the 
      ' user picked at least one file. If the .Show method returns 
      ' False, the user clicked Cancel. 
      If .Show = True Then 
 
         'Loop through each file selected and add it to our list box. 
         For Each varFile In .SelectedItems 
            Me.FileList.AddItem varFile 
         Next 
 
      Else 
         MsgBox "You clicked Cancel in the file dialog box." 
      End If 
   End With 
End Sub
```


## Ver também


#### Conceitos


[Objeto Application](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Outros recursos


[Membros do objeto Application](3ab5276c-d52a-72a9-244c-ec92ead48811.md)