
# Propriedade Form.Recordset (Access)

Retorna ou define o objeto  **Recordset** do ADO ou **[Recordset](http://msdn.microsoft.com/library/9774232C-E6DA-175B-FC7F-ED2AB7908FA0%28Office.15%29.aspx)** do DAO que representa a fonte de registro do objeto especificado. **Object** de leitura/gravação.


## Sintaxe

 _expressão_. **Recordset**

 _expressão_ Uma variável que representa um objeto **Form**.


## Comentários

A propriedade  **Recordset** retorna o objeto conjunto de registros que fornece os dados que estão sendo procurados em um formulário, relatório, controle de caixa de listagem ou controle de caixa de combinação. Se um formulário se baseia em uma consulta, por exemplo, ao referir-se à propriedade **Recordset**, isso equivale a clonar um objeto **Recordset** usando a mesma consulta. No entanto, ao contrário de usar a propriedade **RecordsetClone**, alterar o registro atual no conjunto de registros retornado pela propriedade **Recordset** do formulário também define o registro atual do formulário.

O comportamento leitura/gravação de  **Recordset** é determinado pelo tipo do conjunto de registros (ADO ou DAO) e o tipo de dado (Access ou SQL) contidos no conjunto de registros identificado pela propriedade.



|**Tipo do conjunto de registros**|**Baseado em dados SQL (Structure Query Language, linguagem de consulta estruturada)**|**Baseado em dados armazenados pelo mecanismo de banco de dados do Access**|
|:-----|:-----|:-----|
|**ADO**|Leitura/gravação|Leitura/gravação|
|**DAO**|N/A|Leitura/gravação|
O exemplo a seguir abre um formulário, abre um conjunto de registros e vincula o formulário ao conjunto de registros configurando a propriedade  **Recordset** do formulário como o objeto **Recordset** recentemente criado.




```
Global rstSuppliers As ADODB.Recordset 
Sub MakeRW()      
    DoCmd.OpenForm "Suppliers" 
    Set rstSuppliers = New ADODB.Recordset 
    rstSuppliers.CursorLocation = adUseClient 
    rstSuppliers.Open "Select * From Suppliers", _ 
         CurrentProject.Connection, adOpenKeyset, adLockOptimistic      
    Set Forms("Suppliers").Recordset = rstSuppliers 
End Sub
```

Use a propriedade  **Recordset**:


- Para usar métodos com o objeto  **Recordset** para os quais não há suporte direto nos formulários. Por exemplo, use a propriedade **Recordset** com os métodos **Find** do ADO ou **Find** do DAO em uma caixa de diálogo personalizada para encontrar um registro.
    
- Para ajustar uma transação (que pode ser revertida) em um conjunto de alterações de edição que afetam várias formulários.
    
Alterar a propriedade  **Recordset** de um formulário também pode alterar as propriedades **RecordSource**, **RecordsetType** e **RecordLocks**. Além disso, algumas propriedades relacionadas a dados podem ser substituídas. Por exemplo, as propriedades **Filter**, **FilterOn**, **OrderBy** e **OrderByOn**.

Chamar o método  **Requery** de um conjunto de registros de um formulário (por exemplo, `Forms(0).Recordset.Requery`) pode desacoplar o formulário. Para atualizar os dados em um formulário acoplado a um conjunto de registros, defina a propriedade  **RecordSource** do formulário como ela mesma ( `Forms(0).RecordSource = Forms(0).RecordSource`).


- Para acoplar vários formulários a um conjunto de dados em comum. Isso permite a sincronização de vários formulários. Por exemplo
    



```
   Set Me.Recordset = Forms!Form1.Recordset
```

Quando um formulário é acoplado a um conjunto de registros, ocorre um erro se você usar o comando Filtrar por formulário.


## Exemplo

O exemplo a seguir usa a propriedade  **Recordset** para criar uma nova cópia do objeto **Recordset** do formulário atual e imprime os nomes dos campos contidos na janela Depuração.


```
Sub Print_Field_Names() 
    Dim rst As DAO.Recordset, intI As Integer 
    Dim fld As Field 
 
    Set rst = Me.Recordset 
    For Each fld in rst.Fields 
        ' Print field names. 
        Debug.Print fld.Name 
    Next 
End Sub
```

O exemplo a seguir usa a propriedade  **Recordset** e o objeto **Recordset** para sincronizar um conjunto de registros com o registro atual do formulário. Quando o nome de uma empresa é selecionado de uma caixa de combinação, o método **FindFirst** é usado para localizar o registro dessa empresa, fazendo com que o formulário exiba o registro encontrado.




```
Sub SupplierID_AfterUpdate() 
    Dim rst As DAO.Recordset 
    Dim strSearchName As String 
 
    Set rst = Me.Recordset 
    strSearchName = CStr(Me!SupplierID) 
    rst.FindFirst "SupplierID = " &amp; strSearchName 
    If rst.NoMatch Then 
        MsgBox "Record not found" 
    End If 
    rst.Close 
End Sub
```

O código a seguir ajuda a determinar o tipo de conjunto de registros retornado pela propriedade  **Recordset** em diferentes condições.




```
Sub CheckRSType() 
    Dim rs as Object 
 
    Set rs=Forms(0).Recordset 
    If TypeOf rs Is DAO.Recordset Then 
        MsgBox "DAO Recordset" 
    ElseIf TypeOf rs is ADODB.Recordset Then 
        MsgBox "ADO Recordset" 
    End If 
End Sub
```


## Ver também


#### Conceitos


[Objeto Form](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Outros recursos


[Membros do objeto Form](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)