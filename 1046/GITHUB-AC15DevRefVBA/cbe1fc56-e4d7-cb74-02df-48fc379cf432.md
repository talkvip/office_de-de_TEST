
# Método Application.DLookup (Access)

Você pode usar a função  **DLookup** para obter o valor de um campo em particular de um conjunto de registros especificado (um domínio).


## Sintaxe

 _expressão_. **DLookup**( ** _Expr_**, ** _Domain_**, ** _Criteria_** )

 _expressão_ Uma variável que representa um objeto **Application**.


### Parâmetros



|**Nome**|**Obrigatório/Opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _Expr_|Obrigatório|**String**|Uma expressão que identifica o campo cujo valor você deseja retornar. Pode ser uma expressão de cadeia de caracteres que identifica um campo de uma tabela ou de uma consulta ou pode ser uma expressão que executa um [cálculo de dados nesse campo](http://msdn.microsoft.com/library/73c27d1c-0a3c-03e4-c17c-337133d7b316%28Office.15%29.aspx). Em  _expr_, você pode incluir o nome de um campo de uma tabela, um controle de um formulário, uma constante ou uma função. Se  _expr_ incluir uma função, ela poderá ser interna ou definida pelo usuário, mas nenhuma outra função de agregação de domínio ou de agregação SQL.|
| _Domain_|Obrigatório|**String**|Uma expressão de cadeia de caracteres que identifica o conjunto de registros que constitui o domínio. Pode ser um nome de tabela ou um nome de uma consulta que não exija um parâmetro.|
| _Criteria_|Opcional|**Variant**|Uma expressão de cadeia de caracteres opcional usada para restringir o intervalo de dados no qual a função  **DLookup** é executada. Por exemplo, _criteria_ com frequência é equivalente à cláusula WHERE em uma expressão SQL sem a palavra WHERE. Se _criteria_ for omitido, a função **DLookup** avalia _expr_ no domínio inteiro. Todos os campos incluídos em _criteria_ também deverão ser um campo em _domain_; caso contrário, a função  **DLookup** retornará **Null**.|

### Valor de retorno

Variante


## Comentários

Você pode usar a função  **DLookup** para exibir o valor de um campo que não esteja na origem do registro para seu formulário ou relatório. Por exemplo, suponha que você tenha um formulário baseado em uma tabela Outros Detalhes. O formulário exibe os campos OrderID, ProductID, UnitPrice, Quantity e Discount. Entretanto, o campo ProductName está em outra tabela, a tabela Produtos. Você poderia usar a função **DLookup** em um controle calculado para exibir o ProductName no mesmo formulário.

A função  **DLookup** retorna um valor de campo único baseado nas informações especificadas em _criteria_. Embora  _criteria_ seja um argumento opcional, se você não fornecer um valor para _criteria_, a função  **DLookup** retornará um valor aleatório no domínio.

Se nenhum registro satisfizer  _criteria_ ou se _domain_ não contiver registros, a função **DLookup** retornará **Null**.

Se mais de um campo atender aos  _criteria_, a função  **DLookup** retornará a primeira ocorrência. Você deve especificar critérios que garantirão que o valor do campo retornado pela função **DLookup** seja exclusivo. Você poderá querer usar um valor de chave primária para seus critérios, como `[EmployeeID]` no exemplo a seguir, para garantir que a função **DLookup** retorne um valor exclusivo:




```
Dim varX As Variant 
varX = DLookup("[LastName]", "Employees", "[EmployeeID] = 1")
```

Se você usar a função  **DLookup** em uma macro ou em um módulo, em uma expressão de consulta ou em um controle calculado, deverá criar o argumento _criteria_ cuidadosamente para garantir que ele seja avaliado de forma correta.

Você pode usar a função  **DLookup** para especificar critérios na linha Critérios de uma consulta, em uma expressão de campo calculado em uma consulta ou na linha Atualizar para em uma consulta de atualização.

Você também pode usar a função  **DLookup** em uma expressão em um controle calculado em um formulário ou relatório caso o campo que deve ser exibido não esteja na fonte do registro na qual seu formulário ou relatório se baseia. Por exemplo, suponha que você tenha um formulário Detalhes do Pedido baseado em uma tabela Detalhes do Pedido com uma caixa de texto chamada ProductID que exibe o campo ProductID. Para procurar ProductName em uma tabela Produtos com base no valor na caixa de texto, você poderia criar outra caixa de texto e definir sua propriedade **ControlSource** para a seguinte expressão:




```
=DLookup("[ProductName]", "Products", "[ProductID] =" _ 
     &amp; Forms![Order Details]!ProductID)
```

 **Dicas**


- Embora você possa usar a função  **DLookup** para exibir um valor de um campo em uma tabela estrangeira, pode ser mais eficiente criar uma consulta que contenha os campos de que você precisa em ambas as tabelas e então basear seu formulário ou relatório nessa consulta.
    
- Você também pode usar o Assistente de Pesquisa para localizar valores em uma tabela estrangeira.
    
 **Links fornecidos por:**
![Ícone de Membro da Comunidade](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) A comunidade[UtterAccess](http://www.utteraccess.com)


- [Formulários: preencher controles/caixas de texto com base em uma seleção de caixa de combinação](http://www.utteraccess.com/wiki/index.php/Forms:_Populate_Controls/Text_Boxes_Based_on_Combobox_Selection)
    
- [Pesquisar um valor em todas as tabelas/todos os campos em um banco de dados](http://www.utteraccess.com/wiki/index.php/Search:_All_Tables_All_Fields)
    

## Exemplo

O exemplo a seguir retorna informações de nome do campo CompanyName do registro que satisfaz  _criteria_. O domínio é uma tabela Remetentes. O argumento  _criteria_ restringe o conjunto de registros resultante àqueles para os quais ShipperID é igual a 1.


```
Dim varX As Variant 
varX = DLookup("[CompanyName]", "Shippers", "[ShipperID] = 1")
```

O próximo exemplo da tabela Remetentes usa o controle de formulário ShipperID para fornecer critérios para a função  **DLookup**. Observe que a referência ao controle não está incluída nas aspas que denotam as cadeias de caracteres. Isso garante que sempre que a função **DLookup** for chamada, o Microsoft Access obterá o valor atual do controle.




```
Dim varX As Variant 
varX = DLookup("[CompanyName]", "Shippers", "[ShipperID] = " _ 
    &amp; Forms!Shippers!ShipperID)
```

O próximo exemplo usa uma variável,  `intSearch`, para obter o valor.




```
Dim intSearch As Integer 
Dim varX As Variant 
 
intSearch = 1 
varX = DLookup("[CompanyName]", "Shippers", _ 
    "[ShipperID] = " &amp; intSearch)
```



Os exemplos a seguir mostram como usar os diversos tipos de critérios com a função  **DLookup**.

 **Código de exemplo fornecido por:**
![Ícone de Membro da Comunidade](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) A comunidade[UtterAccess](http://www.utteraccess.com)




```
    ' ***************************
    ' Typical Use
    ' Numerical values. Replace "number" with the number to use.
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = number")

    ' Strings.
    ' Numerical values. Replace "string" with the string to use.
    variable = DLookup("[FieldName]", "TableName", "[Criteria]= 'string'")

    ' Dates. Replace "date" with the string to use.
    variable = DLookup("[FieldName]", "TableName", "[Criteria]= #date#")
    ' ***************************

    ' ***************************
    ' Referring to a control on a form
    ' Numerical values
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = " &amp; Forms!FormName!ControlName)

    ' Strings
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = '" &amp; Forms!FormName!ControlName &amp; "'")

    ' Dates
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = #" &amp; Forms!FormName!ControlName &amp; "#")
    ' ***************************

    ' ***************************
    ' Combinations
    ' Multiple types of criteria
    variable = DLookup("[FieldName]", "TableName", "[Criteria1] = " &amp; Forms![FormName]![Control1] _
             &amp; " AND [Criteria2] = '" &amp; Forms![FormName]![Control2] &amp; "'" _
            &amp; " AND [Criteria3] =#" &amp; Forms![FormName]![Control3] &amp; "#")
    
    ' Use two fields from a single record.
    variable = DLookup("[LastName] &amp; ', ' &amp; [FirstName]", "tblPeople", "[PrimaryKey] = 7")
            
    ' Expressions
    variable = DLookup("[Field1] + [Field2]", "tableName", "[PrimaryKey] = 7")
    
    ' Control Structures
    variable = DLookup("IIf([LastName] Like 'Smith', 'True', 'False')", "tableName", "[PrimaryKey] = 7")
    ' ***************************
```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

UtterAccess é o fórum principal de wiki e a Ajuda do Microsoft Access. clique aqui para ingressar.


## Ver também
<a name="AboutContributors"> </a>


#### Conceitos


[Objeto Application](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Outros recursos


[Membros do objeto Application](3ab5276c-d52a-72a9-244c-ec92ead48811.md)