
# Método DoCmd.TransferText (Access)

O método  **TransferText** executa a ação **TransferText** no Visual Basic.


## Sintaxe

 _expressão_. **TransferText**( ** _TransferType_**, ** _SpecificationName_**, ** _TableName_**, ** _FileName_**, ** _HasFieldNames_**, ** _HTMLTableName_**, ** _CodePage_** )

 _expressão_ Uma variável que representa um objeto **DoCmd**.


### Parâmetros



|**Nome**|**Obrigatório/opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _TransferType_|Opcional|**[AcTextTransferType](56aeb63e-e249-9267-3184-9a370f766fd2.md)**|O tipo de transferência que você deseja fazer. Você pode importar dados de, exportar dados para ou vincular aos dados em arquivos de texto de largura fixa ou em arquivos HTML. O valor padrão é  **acImportDelim**. Somente os tipos de transferência **acImportDelim**, **acImportFixed**, **acExportDelim**, **acExportFixed** ou **acExportMerge** têm suporte em um projeto do Microsoft Access (.adp).|
| _SpecificationName_|Opcional|**Variant**|Uma expressão de cadeia de caracteres que é o nome de uma especificação de importação ou de exportação que você criou e salvou no banco de dados atual. Para um arquivo de texto de largura fixa, você deverá especificar um argumento ou usar um arquivo schema.ini, que deverá ser armazenado na mesma pasta que o arquivo de texto importado, vinculado ou exportado. Para criar um arquivo de esquema, você poderá usar o assistente de importação/exportação de texto para criar o arquivo. Para os arquivos de texto delimitados e arquivos de dados de mala direta do Microsoft Word, você poderá deixar este argumento em branco para selecionar as especificações de importação/exportação padrão.|
| _TableName_|Opcional|**Variant**|Uma expressão de cadeia de caracteres que é o nome da tabela do Microsoft Access para a qual você deseja importar dados de texto, exportar dados de texto ou vincular dados de texto, ou a consulta do Microsoft Access cujos resultados você deseja exportar para um arquivo de texto.|
| _FileName_|Opcional|**Variant**|Uma expressão de cadeia de caracteres que é o nome completo, incluindo o caminho, do arquivo de texto do qual você deseja importar, exportar para ou ao qual deseja vincular.|
| _HasFieldNames_|Opcional|**Variant**|Use  **True** (?1) para usar a primeira linha do arquivo de texto como nomes de campo ao importar, exportar ou vincular. Use **False** (0) para tratar a primeira linha do arquivo de texto como dados normais. Se você deixar esse argumento em branco, o padrão ( **False** ) será considerado. Esse argumento será ignorado para os arquivos de dados de mala direta do Microsoft Word, que sempre deverão conter os nomes de campo na primeira linha.|
| _HTMLTableName_|Opcional|**Variant**|Uma expressão de cadeia de caracteres que é o nome da tabela ou da lista no arquivo HTML que você deseja importar ou vincular. Esse argumento será ignorado, a menos que o argumento  _TransferType_ seja definido como **acImportHTML** ou **acLinkHTML**. Se você deixar esse argumento em branco, a primeira tabela ou lista no arquivo HTML será importada ou vinculada. O nome da tabela ou da lista no arquivo HTML é determinado pelo texto especificado pela marca **<CAPTION>**, se houver uma marca  **<CAPTION>**. Se não houver uma marca  **<CAPTION>**, o nome será determinado pelo texto especificado pela marca  **<TITLE>**. Se mais de uma tabela ou lista tiver o mesmo nome, o Microsoft Access as distinguirá adicionando um número ao final do nome de cada tabela ou lista; por exemplo, Funcionários1 e Funcionários2.|
|CodePage[](https://msdn.microsoft.com/pt-br/library/windows/desktop/dd317756%28v=vs.85%29.aspx)|Opcional|**Variant**|Um valor  **Long** que indica o conjunto de caracteres da página de código.|

## Comentários

Você pode usar o método  **TransferText** para importar ou exportar texto entre o banco de dados do Microsoft Access ou projeto do Access (.adp) e um arquivo de texto. Você também pode vincular os dados em um arquivo de texto para o banco de dados do Access atual. Com um arquivo de texto vinculado, você pode exibir os dados de texto com o Access e ainda permitir o acesso completo aos dados do seu programa de processamento de texto. Você também poderá importar de, importar para e vincular a uma tabela ou uma lista em um arquivo HTML (*.html).

Você pode exportar os dados em consultas seleção do Access para arquivos de texto. O Access exporta o conjunto de resultados da consulta, tratando-o como uma tabela.


## Exemplo

O exemplo a seguir exporta os dados do Relatório Externo da tabela do Microsoft Access para o arquivo de texto delimitado Abril.doc usando a especificação Saída Padrão:


```
DoCmd.TransferText acExportDelim, "Standard Output", _ 
    "External Report", "C:\Txtfiles\April.doc"
```

O código a seguir mostra como criar um novo documento do Microsoft Word e executar uma mala direta com os dados armazenados na tabela Clientes.

 **Código de exemplo fornecido por:** A[Referência do programador do Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)




```
Public Sub DoMailMerge(strFileSavePath As String)

    ' Create new Word App, add a document and set it visible
    Dim wdApp As New Word.Application
    wdApp.Documents.Add
    wdApp.Visible = True

    ' Open the data set from this database
    wdApp.ActiveDocument.MailMerge.OpenDataSource _
        Name:=Application.CurrentProject.FullName, _
        OpenExclusive:=False, _
        LinkToSource:=True, _
        Connection:="TABLE Customers", _
        SQLStatement:="SELECT Customers.* FROM Customers;"
              
    ' Add fields to the mail merge document
    Dim oSel As Object
    Set oSel = wdApp.Selection
    With wdApp.ActiveDocument.MailMerge.Fields
    
        oSel.TypeText vbNewLine &amp; vbNewLine
        .Add oSel.range, "First_Name"
        oSel.TypeText " "
        .Add oSel.range, "Last_Name"
        oSel.TypeText vbNewLine
        .Add oSel.range, "Company"
        oSel.TypeText vbNewLine
        .Add oSel.range, "Address"
        oSel.TypeText vbNewLine
        .Add oSel.range, "City"
        oSel.TypeText ", "
        .Add oSel.range, "State"
        oSel.TypeText " "
        .Add oSel.range, "Zip"
        oSel.TypeText vbNewLine
        oSel.TypeParagraph
        oSel.TypeText "Dear "
        .Add oSel.range, "First_Name"
        oSel.TypeText ","
        oSel.TypeText vbNewLine
        oSel.TypeParagraph
        oSel.TypeText "We have created this mail just for you..."
        oSel.TypeText vbNewLine
        oSel.TypeText vbNewLine
        oSel.TypeText "Sincerely," &amp; vbNewLine &amp; "John Q. Public"
        oSel.TypeText vbFormFeed
        
    End With
    
    ' Execute the mail merge and save the document
    wdApp.ActiveDocument.MailMerge.Execute
    wdApp.ActiveDocument.SaveAs strFileSavePath
        
    ' Close everything and Cleanup Variables
    Set oSel = Nothing
    wdApp.ActiveDocument.Close False
    Set wdApp = Nothing

End Sub
```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

Wrox Press é impulsionada pelo programador para programador filosofia. Livros Wrox são escritos por programadores para programadores, ea marca Wrox significa soluções autoritárias para problemas de programação do mundo real.


## Ver também
<a name="AboutContributors"> </a>


#### Conceitos


[Objeto DoCmd](3ce44cca-9979-0a1e-9787-079a52ce528f.md)
#### Outros recursos


[Membros do objeto DoCmd](3e7ade9e-86e4-0751-188b-5d31c9101651.md)