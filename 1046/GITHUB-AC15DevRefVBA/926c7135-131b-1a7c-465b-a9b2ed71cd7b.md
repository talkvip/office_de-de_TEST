
# Método DoCmd.ApplyFilter (Access)

O método  **ApplyFilter** executa a ação **ApplyFilter** no Visual Basic.


## Sintaxe

 _expressão_. **ApplyFilter**( ** _FilterName_**, ** _WhereCondition_**, ** _ControlName_** )

 _expressão_ Uma variável que representa um objeto **DoCmd**.


### Parâmetros



|**Nome**|**Obrigatório/opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _FilterName_|Opcional|**Variant**|Uma expressão de cadeia de caracteres que é o nome válido de um filtro ou de uma consulta no banco de dados atual. Ao usar esse método para aplicar um filtro de servidor, o argumento  _FilterName_ deve estar em branco.|
| _WhereCondition_|Opcional|**Variant**|Uma expressão de cadeia de caracteres que é uma cláusula WHERE do SQL válida sem a palavra WHERE.|
| _ControlName_|Opcional|**Variant**||

## Comentários

Você pode usar a ação ApplyFilter para aplicar um filtro, uma consulta ou uma cláusula WHERE do SQL a uma tabela, um formulário ou um relatório para restringir ou classificar os registros da tabela ou os registros da tabela base ou da consulta do formulário ou relatório. Em relatórios, você pode usar essa ação apenas em uma macro especificada pela propriedade de evento  **OnOpen** do relatório.

Você pode usar essa ação para aplicar uma cláusula WHERE do SQL apenas ao aplicar um filtro de servidor. Um filtro de servidor não pode ser aplicado a uma fonte de registros do procedimento armazenado.


 **Observação**  Você poderá usar o argumento Filter Name se já tiver definido um filtro que forneça os dados apropriados. É possível usar o argumento Where Condition para inserir diretamente os critérios de restrição. Se você usar ambos os argumentos, o Microsoft Access aplicará a cláusula WHERE aos resultados do filtro. É preciso usar um ou ambos os argumentos.

Você pode aplicar um filtro ou uma consulta a um formulário no modo de formulário ou no modo de folha de dados.

O filtro e a condição WHERE aplicados tornam-se a configuração da propriedade  **[Filter](5eb49f82-8519-981c-a663-9862736ac95f.md)** do formulário ou da propriedade **[ServerFilter](e73ad797-8c76-705f-080b-2d0f3423cb39.md)** do relatório.

Quando você salva uma tabela ou um formulário, o Access salva qualquer filtro definido atualmente nesse objeto, mas não aplicará o filtro automaticamente na próxima vez que o objeto for aberto (embora aplique automaticamente qualquer classificação aplicada ao objeto antes de ser salvo). Para aplicar um filtro automaticamente quando um formulário é aberto pela primeira vez, especifique uma macro contendo a ação ApplyFilter ou um procedimento de evento contendo o método  **ApplyFilter** do objeto **DoCmd** como a configuração da propriedade de evento **OnOpen** do formulário. Você também pode aplicar um filtro usando a ação OpenForm ou OpenReport ou os métodos correspondentes. Para aplicar um filtro automaticamente quando uma tabela é aberta pela primeira vez, abra a tabela usando uma macro contendo a ação OpenTable, seguida imediatamente da ação ApplyFilter.

É necessário incluir, pelo menos, um dos dois argumentos do método  **ApplyFilter**. Quando você insere um valor em ambos os argumentos, o argumento _WhereCondition_ é aplicado ao filtro.

O comprimento máximo do argumento  _WhereCondition_ é de 32.768 caracteres (ao contrário do argumento da ação Where Condition na janela Macro, cujo comprimento máximo é de 256 caracteres).


## Exemplo

O exemplo a seguir usa o método  **ApplyFilter** para exibir somente registros que contêm o nome "King" no campo LastName:


```
DoCmd.ApplyFilter , "LastName = 'King'"
```



O exemplo a seguir mostra como usar a propriedade  **ApplyFilter** para filtrar os registros exibidos quando um botão de alternância denominado tglFilter é clicado.

 **Código de exemplo fornecido por:** Bill Jelen,[MrExcel.com](http://www.mrexcel.com/)




```
Private Sub tglFilter_Click()
    With tglFilter
        If .Value = True Then
            .Caption = "P/T"
            .StatusBarText = "only full-timers"
            DoCmd.ApplyFilter , "[Hours]=40"
        ElseIf .Value = False Then
            .Caption = "All"
            .StatusBarText = "only part-timers"
            DoCmd.ApplyFilter , "[Hours]<40"
        Else
            .Caption = "F/T"
            .StatusBarText = "all employees"
            DoCmd.ShowAllRecords
            .SetFocus 'to activate the button's statusbar-text
        End If
    End With
End Sub
```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

Holy Macro! Livros publica livros de entretenimento para as pessoas que usam o Microsoft Office. Veja o catálogo completo em MrExcel.com.


## Ver também
<a name="AboutContributors"> </a>


#### Conceitos


[Objeto DoCmd](3ce44cca-9979-0a1e-9787-079a52ce528f.md)
#### Outros recursos


[Membros do objeto DoCmd](3e7ade9e-86e4-0751-188b-5d31c9101651.md)