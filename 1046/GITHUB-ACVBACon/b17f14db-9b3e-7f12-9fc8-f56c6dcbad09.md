
# Localizar um registro em um conjunto de registros DAO do tipo tabela

Você usa o método  **[Seek](http://msdn.microsoft.com/library/EF83D909-C962-B016-7D33-36EACDC25C2C%28Office.15%29.aspx)** para localizar um registro em um objeto **[Recordset](http://msdn.microsoft.com/library/9774232C-E6DA-175B-FC7F-ED2AB7908FA0%28Office.15%29.aspx)** do tipo tabela.

Quando você usa o método  **Seek** para localizar um registro, o mecanismo de banco de dados do Access usa o índice atual da tabela, conforme definido pela propriedade **[Index](http://msdn.microsoft.com/library/54626DE0-EB51-31F2-BF24-E29CBFBBAA02%28Office.15%29.aspx)**.

 **Observação**  Se você usar o método  **Seek** em um objeto **Recordset** do tipo tabela sem primeiro definir o índice atual, ocorrerá um erro de tempo de execução.

O exemplo a seguir abre um objeto  **Recordset** tipo de tabela denominado Funcionários e usa o método Busca para localizar o registro que contém um valor de **lngEmpID** no campo EmployeeID. Ele retorna a data de contratação do funcionário especificado.



```
Function GetHireDate(lngEmpID As Long) As Variant 
 
Dim dbsNorthwind As DAO.Database 
Dim rstEmployees As DAO.Recordset 
 
On Error GoTo ErrorHandler 
 
   Set dbsNorthwind = CurrentDB 
   Set rstEmployees = dbsNorthwind.OpenRecordset("Employees") 
 
   ' The index name for Employee ID. 
   rstEmployees.Index = "PrimaryKey" 
   rstEmployees.Seek "=", lngEmpID 
 
   If rstEmployees.NoMatch Then 
      GetHireDate = Null 
   Else 
      GetHireDate = rstEmployees!HireDate 
   End If 
 
   rstEmployees.Close 
   dbsNorthwind.Close 
 
   Set rstEmployees = Nothing 
   Set dbsNorthwind = Nothing 
 
Exit Function 
 
ErrorHandler: 
   MsgBox "Error #: " &amp; Err.Number &amp; vbCrLf &amp; vbCrLf &amp; Err.Description 
End Function 

```

O método  **Seek** sempre começa procurando os registros no início do objeto **Recordset**. Se você usar o método **Seek** com os mesmos argumentos mais de uma vez no mesmo **Recordset**, ele encontrará o mesmo registro.
Você pode usar a propriedade  **[NoMatch](http://msdn.microsoft.com/library/47D03575-F570-89B5-A20F-A3BD8B8B5C6D%28Office.15%29.aspx)** no objeto **Recordset** para testar se um registro que corresponda aos critérios da pesquisa foi encontrado. Se o registro que corresponde aos critérios tiver sido encontrado, a propriedade **NoMatch** será **False**; caso contrário, ela será **True**.
O exemplo de código a seguir mostra como você pode criar uma função que usa o método  **Seek** para localizar um registro usando um índice de campo múltiplo.



```
Function GetFirstPrice(lngOrderID As Long, lngProductID As Long) As Variant 
 
Dim dbsNorthwind As DAO.Database 
Dim rstOrderDetail As DAO.Recordset 
 
On Error GoTo ErrorHandler 
 
   Set dbsNorthwind = CurrentDb 
   Set rstOrderDetail = dbsNorthwind.OpenRecordset("Order Details") 
 
   rstOrderDetail.Index = "PrimaryKey" 
   rstOrderDetail.Seek "=", lngOrderID, lngProductID 
 
   If rstOrderDetail.NoMatch Then 
      GetFirstPrice = Null 
   Else 
      GetFirstPrice = rstOrderDetail!UnitPrice 
   End If 
 
   rstOrderDetail.Close 
   dbsNorthwind.Close 
 
   Set rstOrderDetail = Nothing 
   Set dbsNorthwind = Nothing 
 
Exit Function 
 
ErrorHandler: 
   MsgBox "Error #: " &amp; Err.Number &amp; vbCrLf &amp; vbCrLf &amp; Err.Description 
End Function 

```

Neste exemplo, a chave primária da tabela consiste em dois campos: OrderID e ProductID. Quando você chama a função GetFirstPrice com uma combinação válida (existente) de valores do campo de OrderID e de ProductID, a função retorna o preço unitário do registro encontrado. Se ela não conseguir localizar a combinação de valores de campo que você deseja na tabela, a função retornará o valor  **Null**.
Se o índice atual for um índice de campo múltiplo, os valores chave à direita poderão ser omitidos e serão tratados como valores  **Null**. Ou seja, você pode deixar qualquer número de valores chave no final de um argumento _chave_ de um método **Seek**, mas não no início ou no meio. Entretanto, se você não especificar todos os valores do índice, só poderá usar a cadeia de caracteres de comparação ">" ou "<" com o método **Seek**.
