
# Criar um menu de atalho para um formulário, controle de formulário ou relatório

 **Código de exemplo fornecido por:** Edwin Blancovitch,[Advanced Developers.net](http://advdev.net/)

Quando você está projetando um formulário ou relatório, convém fornecer um método para que o usuário utilize facilmente um comando que se aplica apenas ao contexto atual. Uma maneira de fazer isso é criar um menu de atalho personalizado e aplicá-lo ao controle ou relatório de formulário. O menu de atalho é exibido quando o usuário clica com o botão direito do mouse no objeto ao qual o menu de atalho é aplicado.

Nas versões anteriores do  **Access**, você pode usar a caixa de diálogo  **Personalizar** para criar menus de atalho personalizados. No Access 2013, use um código do Visual Basic for Applications (VBA) para criar menus de atalho. Este artigo descreve como criar um menu de atalho usando o VBA.

Para criar um menun de atalho, é necessário primeiro criar um objeto  **[CommandBar](http://msdn.microsoft.com/library/78603954-40aa-64cb-c407-2e0820d65231%28Office.15%29.aspx)**. O objeto **CommandBar** representa o menu de atalho. Em seguida, use o método **[Add](http://msdn.microsoft.com/library/53e2b0b9-b11a-bf52-a1a3-523aae2c35d8%28Office.15%29.aspx)** para criar objetos **[CommandBarControl](http://msdn.microsoft.com/library/b104ec00-beeb-a927-4b7b-108f4e3164f5%28Office.15%29.aspx)**. Sempre que criar um objeto CommandBarControl, um comando é adicionado ao menu de atalho.
O exemplo a seguir cria o menu de atalho chamado  **SimpleShortcutMenu**, que inclui dois comandos, **Remove Filter/Sort** e **Filter by Selection**.

 **Observação**  Para usar os exemplos a seguir, defina uma referência para o  **Microsoft Office 15.0 Object Library**. Confira o artigo[Definir referências para bibliotecas de tipos](6314a89b-89e9-d8c1-5964-889a361afcd1.md) para saber mais sobre como definir referências.




```
 
Sub CreateSimpleShortcutMenu() 
    Dim cmbShortcutMenu As Office.CommandBar 
     
    ' Create a shortcut menu named "SimpleShortcutMenu. 
    Set cmbShortcutMenu = CommandBars.Add("SimpleShortcutMenu", msoBarPopup, False, True) 
     
    ' Add the Remove Filter/Sort command. 
    cmbShortcutMenu.Controls.Add Type:=msoControlButton, Id:=605 
 
    ' Add the Filter By Selection command. 
    cmbShortcutMenu.Controls.Add Type:=msoControlButton, Id:=640 
     
    Set cmbShortcutMenu = Nothing 
     
End Sub
```

Quando executar o código, o menu de atalho é salvo como parte do banco de dados. Você não precisa executar o mesmo código para criar novamente o menu de atalho a cada vez que abrir o banco de dados.
Para atribuir o menu de atalho a um formulário, controle de formulário ou relatório, defina a propriedade  **Menu de Atalho** do objeto como **Yes** e a propriedade **Barra de Menus de Atalho** como o nome do menu de atalho. Para esse exemplo, defina a apropriedade **Barra de Menus de Atalho** como **SimpleShortcutMenu**.
O exemplo a seguir cria um menu de atalho chamado  **cmdFormFiltering**, que inclui comandos úteis para usar com fomulários contínuos. Neste exemplo, a propriedade **BeginGroup** é usada em diversos controles para agrupá-los visualmente.



```
 
Sub CreateShortcutMenuWithGroups() 
    Dim cmbRightClick As Office.CommandBar 
 
 ' Create the shortcut menu. 
    Set cmbRightClick = CommandBars.Add("cmdFormFiltering", msoBarPopup, False, True) 
     
    With cmbRightClick 
        ' Add the Find command. 
        .Controls.Add msoControlButton, 141, , , True 
         
        ' Start a new grouping and add the Sort Ascending command. 
        .Controls.Add(msoControlButton, 210, , , True).BeginGroup = True 
         
        ' Add the Sort Descending command. 
        .Controls.Add msoControlButton, 211, , , True 
         
        ' Start a new grouping and add the Remove Filer/Sort command. 
        .Controls.Add(msoControlButton, 605, , , True).BeginGroup = True 
         
        ' Add the Filter by Selection command. 
        .Controls.Add msoControlButton, 640, , , True 
         
        ' Add the Filter Excluding Selection command. 
        .Controls.Add msoControlButton, 3017, , , True 
         
        ' Add the Between... command. 
        .Controls.Add msoControlButton, 10062, , , True 
    End With 
 
Set cmbRightClick = Nothing 
End Sub
```

O exemplo a seguir cria um menu de atalho chamado  **cmdReportRightClick**, que inclui comandos úteis para usar com um relatório. Este exemplo mostra como alterar a propriedade **Caption** de cada controle, à medida que são adicionados ao menu de atalho.



```
 
Sub CreateReportShortcutMenu() 
    Dim cmbRightClick As Office.CommandBar 
    Dim cmbControl As Office.CommandBarControl 
 
   ' Create the shortcut menu. 
    Set cmbRightClick = CommandBars.Add("cmdReportRightClick", msoBarPopup, False, True) 
 
    With cmbRightClick 
         
        ' Add the Print command. 
        Set cmbControl = .Controls.Add(msoControlButton, 2521, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Quick Print" 
         
        ' Add the Print command. 
        Set cmbControl = .Controls.Add(msoControlButton, 15948, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Select Pages" 
         
        ' Add the Page Setup... command. 
        Set cmbControl = .Controls.Add(msoControlButton, 247, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Page Setup" 
         
        ' Add the Mail Recipient (as Attachment)... command. 
        Set cmbControl = .Controls.Add(msoControlButton, 2188, , , True) 
        ' Start a new group. 
        cmbControl.BeginGroup = True 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Email Report as an Attachment" 
         
        ' Add the PDF or XPS command. 
        Set cmbControl = .Controls.Add(msoControlButton, 12499, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Save as PDF/XPS" 
         
        ' Add the Close command. 
        Set cmbControl = .Controls.Add(msoControlButton, 923, , , True) 
        ' Start a new group. 
        cmbControl.BeginGroup = True 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Close Report" 
    End With 
     
    Set cmbControl = Nothing 
    Set cmbRightClick = Nothing 
End Sub
```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

Edwin Blancovitch é Presidente do [Advanced Developers.net](http://advdev.net/), que são os criadores do [Easy Payroll](http://www.easypayroll.net/), um pacote de software para gerenciar as necessidades de recursos humanos, folha de pagamento, horários, agendamento e participações.

