
# Fazer uma célula piscar

Este exemplo mostra como fazer a célula B2 da planilha 1 piscar, alterando a cor e o texto e alternando entre vermelho e branco, no procedimento  **StartBlinking**. O procedimento **StopBlinking** mostra como interromper a intermitência limpando o valor da célula e definindo a propriedade **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** como branco.

 **Código de exemplo fornecido por:** Tom Urtis,[Atlas Programming Management](http://www.atlaspm.com/)



```
Option Explicit

Public NextBlink As Double
'The cell that you want to blink
Public Const BlinkCell As String = "Sheet1!B2"

'Start blinking
Private Sub StartBlinking()
    Application.Goto Range("A1"), 1
    'If the color is red, change the color and text to white
    If Range(BlinkCell).Interior.ColorIndex = 3 Then
        Range(BlinkCell).Interior.ColorIndex = 0
        Range(BlinkCell).Value = "White"
    'If the color is white, change the color and text to red
    Else
        Range(BlinkCell).Interior.ColorIndex = 3
        Range(BlinkCell).Value = "Red"
    End If
    'Wait one second before changing the color again
    NextBlink = Now + TimeSerial(0, 0, 1)
    Application.OnTime NextBlink, "StartBlinking", , True
End Sub

'Stop blkinking
Private Sub StopBlinking()
    'Set color to white
    Range(BlinkCell).Interior.ColorIndex = 0
    'Clear the value in the cell
    Range(BlinkCell).ClearContents
    On Error Resume Next
    Application.OnTime NextBlink, "StartBlinking", , False
    Err.Clear
End Sub

```


## Sobre o colaborador
<a name="AboutContributor"> </a>

MVP Tom Urtis é o fundador da Atlas Programming Management, uma completa do serviço Microsoft Office e o Excel business solutions empresa no Silicon Valley. João tem mais de 25 anos de experiência em gerenciamento de negócios e desenvolvimento Microsoft Office aplicativos e é co-autor de "Holy Macro! It's 2,500 Excel VBA Examples."

