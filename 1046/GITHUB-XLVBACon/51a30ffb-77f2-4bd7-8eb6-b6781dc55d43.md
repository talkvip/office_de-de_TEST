
# Realçar a célula, linha ou coluna ativa

Os seguintes exemplos de código mostram formas de realçar a célula ativa ou as linhas e colunas que incluem a célula ativa. Esses exemplos usam o evento  **[SelectionChange](183e2ca7-06b2-f689-1f77-182dbfbf1e1d.md)** do objeto **[Worksheet](182b705e-854a-81cc-a4b0-59b942de55ae.md)**.

 **Código de exemplo fornecido por:** Tom Urtis,[Atlas Programming Management](http://www.atlaspm.com/)

## Realçando a célula ativa

O exemplo de código a seguir limpa a cor de todas as células na planilha definindo a propriedade  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** como igual a 0; em seguida, realça a célula ativa definindo a propriedade **ColorIndex** como igual a 8 (turquesa).


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    ' Highlight the active cell
    Target.Interior.ColorIndex = 8
    Application.ScreenUpdating = True
End Sub
```


## Realçando a linha e a coluna inteira que inclui a célula ativa

O exemplo de código a seguir limpa a cor de todas as células na planilha definindo a propriedade  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** como igual a 0; em seguida, realça a linha e a coluna inteira que inclui a célula ativa definindo as propriedades **[EntireRow](9e66da51-6cef-4109-ea4e-2acaad42aa1f.md)** e **[EntireColumn](7be55670-75fd-fb02-dc1a-9d70e3a9d80d.md)**.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    If Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    With Target
        ' Highlight the entire row and column that contain the active cell
        .EntireRow.Interior.ColorIndex = 8
        .EntireColumn.Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## Realçando a linha e a coluna que inclui a célula ativa dentro da região atual

O exemplo de código a seguir limpa a cor de todas as células na planilha definindo a propriedade  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** como igual a 0; em seguida, realça a linha e a coluna que inclui a célula ativa dentro da região atual definindo a propriedade **[CurrentRegion](39277cc5-07ff-8453-7330-b272b365f9dc.md)** do objeto **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)**.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    If IsEmpty(Target) Or Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    With ActiveCell
        ' Highlight the row and column that contain the active cell, within the current region
        Range(Cells(.Row, .CurrentRegion.Column), Cells(.Row, .CurrentRegion.Columns.Count + .CurrentRegion.Column - 1)).Interior.ColorIndex = 8
        Range(Cells(.CurrentRegion.Row, .Column), Cells(.CurrentRegion.Rows.Count + .CurrentRegion.Row - 1, .Column)).Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

MVP Tom Urtis é o fundador da Atlas Programming Management, uma completa do serviço Microsoft Office e o Excel business solutions empresa no Silicon Valley. João tem mais de 25 anos de experiência em gerenciamento de negócios e desenvolvimento Microsoft Office aplicativos e é co-autor de "Holy Macro! It's 2,500 Excel VBA Examples."

