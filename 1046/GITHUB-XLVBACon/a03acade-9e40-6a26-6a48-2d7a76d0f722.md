
# Consultar linhas e colunas

Use a propriedade  **Linhas** ou a propriedade **Colunas** para trabalhar com linhas ou colunas inteiras. Essas propriedades retornam um objeto **Range** que representa um intervalo de células. No exemplo a seguir, `Rows(1)` retorna a linha um da Planilha1. A propriedade **Negrito** do objeto **Fonte** para o intervalo é então definido como **Verdadeiro**.


```
Sub RowBold() 
    Worksheets("Sheet1").Rows(1).Font.Bold = True 
End Sub
```


A tabela a seguir ilustra algumas referências de linha e de coluna usando as propriedades  **Rows** e **Columns**.



|**Referência**|**Significado**|
|:-----|:-----|
| `Rows(1)`|Linha um|
| `Rows`|Todas as linhas da planilha|
| `Columns(1)`|Coluna um|
| `Columns("A")`|Coluna um|
| `Columns`|Todas as colunas da planilha|
Para trabalhar com várias linhas ou colunas ao mesmo tempo, crie uma variável de objeto e use o método  **União**, combinando várias chamadas à propriedade **Linhas** ou **Colunas**. O exemplo a seguir altera o formato da linha um, três e cinco na planilha um da pasta de trabalho ativa para negrito.



```
Sub SeveralRows() 
    Worksheets("Sheet1").Activate 
    Dim myUnion As Range 
    Set myUnion = Union(Rows(1), Rows(3), Rows(5)) 
    myUnion.Font.Bold = True 
End Sub
```

 **Código de exemplo fornecido por:** Dennis Wallentin,[VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)
Este exemplo exclui as linhas vazias de um intervalo selecionado.



```
Sub Delete_Empty_Rows()
    'The range from which to delete the rows.
    Dim rnSelection As Range
    
    'Row and count variables used in the deletion process.
    Dim lnLastRow As Long
    Dim lnRowCount As Long
    Dim lnDeletedRows As Long
    
    'Initialize the number of deleted rows.
    lnDeletedRows = 0
    
    'Confirm that a range is selected, and that the range is contiguous.
    If TypeName(Selection) = "Range" Then
        If Selection.Areas.Count = 1 Then
            
            'Initialize the range to what the user has selected, and initialize the count for the upcoming FOR loop.
            Set rnSelection = Application.Selection
            lnLastRow = rnSelection.Rows.Count
        
            'Start at the bottom row and work up: if the row is empty then
            'delete the row and increment the deleted row count.
            For lnRowCount = lnLastRow To 1 Step -1
                If Application.CountA(rnSelection.Rows(lnRowCount)) = 0 Then
                    rnSelection.Rows(lnRowCount).Delete
                    lnDeletedRows = lnDeletedRows + 1
                End If
            Next lnRowCount
        
            rnSelection.Resize(lnLastRow - lnDeletedRows).Select
         Else
            MsgBox "Please select only one area.", vbInformation
         End If
    Else
        MsgBox "Please select a range.", vbInformation
    End If
    
    'Turn screen updating back on.
    Application.ScreenUpdating = True

End Sub
```

Este exemplo exclui as colunas vazias de um intervalo selecionado.



```
Sub Delete_Empty_Columns()
    'The range from which to delete the columns.
    Dim rnSelection As Range
    
    'Column and count variables used in the deletion process.
    Dim lnLastColumn As Long
    Dim lnColumnCount As Long
    Dim lnDeletedColumns As Long
    
    lnDeletedColumns = 0
    
    'Confirm that a range is selected, and that the range is contiguous.
    If TypeName(Selection) = "Range" Then
        If Selection.Areas.Count = 1 Then
            
            'Initialize the range to what the user has selected, and initialize the count for the upcoming FOR loop.
            Set rnSelection = Application.Selection
            lnLastColumn = rnSelection.Columns.Count
        
            'Start at the far-right column and work left: if the column is empty then
            'delete the column and increment the deleted column count.
            For lnColumnCount = lnLastColumn To 1 Step -1
                If Application.CountA(rnSelection.Columns(lnColumnCount)) = 0 Then
                    rnSelection.Columns(lnColumnCount).Delete
                    lnDeletedColumns = lnDeletedColumns + 1
                End If
            Next lnColumnCount
    
            rnSelection.Resize(lnLastColumn - lnDeletedColumns).Select
        Else
            MsgBox "Please select only one area.", vbInformation
        End If
    Else
        MsgBox "Please select a range.", vbInformation
    End If
    
    'Turn screen updating back on.
    Application.ScreenUpdating = True

End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

Dennis Wallentin é autor do VSTO &amp; .NET &amp; Excel, um blog que se concentra em soluções .NET Framework para o Excel e os serviços do Excel. Dennis desenvolve soluções do Excel para mais de 20 anos e também é co-autor de "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)."

