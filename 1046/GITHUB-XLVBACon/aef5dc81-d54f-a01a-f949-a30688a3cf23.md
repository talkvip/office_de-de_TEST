
# Trabalhando com formas (objetos de desenho)

As formas, ou os objetos de desenho, são representados por três objetos diferentes: a coleção  **[Shapes](f9c6548c-d028-1b70-a11c-c4b45ff19177.md)**, a coleção **[ShapeRange](e1b8229c-73a0-4a77-5e00-4bcec9032260.md)** e o objeto **[Shape](8f01fcd1-b7d9-5216-2de5-40fb6648a403.md)**. Em geral, você usa a coleção **Shapes** para criar formas e para iterar por todas as formas em uma determinada planilha; usa o objeto **Shape** para formatar ou modificar uma única forma e usa a coleção **ShapeRange** para modificar várias formas da mesma maneira como trabalha com várias formas na interface do usuário.


## Configurando as propriedades de uma forma

Várias propriedades de formatação de formas não são definidas por propriedades que se aplicam diretamente ao objeto  **Shape** ou **ShapeRange**. Em vez disso, os atributos de forma relacionados são agrupados sob objetos secundários, como o objeto **FillFormat**, que contém todas as propriedades relacionadas ao preenchimento da forma, ou o objeto **LinkFormat**, que contém todas as propriedades exclusivas de objetos OLE vinculados. Para definir as propriedades de uma forma, primeiro retorne o objeto que representa o conjunto de atributos de forma relacionados e defina as propriedades desse objeto retornado. Por exemplo, use a propriedade **Fill** para retornar o objeto **FillFormat** e defina a propriedade **ForeColor** do objeto **FillFormat** para definir a cor do preenchimento do primeiro plano da forma especificada, como mostrado no exemplo a seguir.


```
Worksheets(1).Shapes(1).Fill.ForeColor.RGB = RGB(255, 0, 0)
```


## Aplicando uma propriedade ou um método a várias formas ao mesmo tempo

Na interface do usuário, você pode executar algumas operações com várias formas selecionadas; por exemplo, é possível selecionar várias formas e definir os preenchimentos individuais de uma só vez. É possível executar outras operações com apenas uma única forma selecionada; por exemplo, você só poderá editar o texto em uma forma se uma única forma estiver selecionada.

No Visual Basic, há duas maneiras de aplicar propriedades e métodos a um conjunto de formas. Essas duas maneiras permitem que você execute qualquer operação que possa ser executada em uma única forma em um intervalo de formas, mesmo que você possa ou não executar a mesma operação na interface do usuário.


- Se a operação funcionar em várias formas selecionadas na interface do usuário, será possível executar a mesma operação no Visual Basic criando uma coleção  **ShapeRange** com as formas com que você deseja trabalhar e aplicando as propriedades e os métodos adequados diretamente à coleção **ShapeRange**.
    
- Se a operação não funcionar em várias formas selecionadas na interface do usuário, ainda será possível executar a operação no Visual Basic criando um loop na coleção  **Shapes** ou por meio de uma coleção **ShapeRange** que contém as formas com que você deseja trabalhar e aplicando as propriedades e os métodos adequados aos objetos **Shape** individuais na coleção.
    
Várias propriedades e métodos que se aplicam ao objeto  **Shape** e à coleção **ShapeRange** falharão se aplicados a determinados tipos de formas. Por exemplo, a propriedade **TextFrame** falhará se aplicada a uma forma que não possa conter texto. Se você não tiver certeza de que cada uma das formas em uma coleção **ShapeRange** possa ter uma determinada propriedade ou método aplicada a ela, não aplique a propriedade ou o método à coleção **ShapeRange**. Se você quiser aplicar uma dessas propriedades ou métodos a uma coleção de formas, deverá criar um loop na coleção e estar cada forma individual para garantir que ela seja do tipo adequado de forma antes de aplicar a propriedade ou o método a ela.


## Criando uma coleção ShapeRange com todas as formas em uma folha

Você pode criar um objeto  **ShapeRange** com todos os objetos **Shape** em uma folha selecionando as formas e então usando a propriedade **ShapeRange** para retornar um objeto **ShapeRange** com as formas selecionadas.


```
Worksheets(1).Shapes.Select 
Set sr = Selection.ShapeRange
```

No Microsoft Excel, o argumento  ** _Index_** não é opcional para a propriedade **Range** da coleção **Shapes** e, portanto, você não poderá usar essa propriedade sem um argumento para criar um objeto **ShapeRange** com todas as formas em uma coleção **Shapes**.


## Aplicando uma propriedade ou um método a uma coleção ShapeRange

Se você puder executar uma operação em várias formas selecionadas na interface do usuário ao mesmo tempo, poderá fazer o equivalente programático ao criar uma coleção  **ShapeRange** e então aplicar as propriedades ou os métodos adequados a ela. O exemplo a seguir cria um intervalo de formas com as formas chamadas "Big Star" e "Little Star" em `myDocument` e aplica um preenchimento de gradiente a elas.


```
Set myDocument = Worksheets(1) 
Set myRange = myDocument.Shapes.Range(Array("Big Star", _ 
 "Little Star")) 
myRange.Fill.PresetGradient _ 
 msoGradientHorizontal, 1, msoGradientBrass
```

A seguir, as diretrizes gerais sobre o comportamento das propriedades e dos métodos quando aplicados a uma coleção  **ShapeRange**.


- A aplicação de um método à coleção é equivalente à aplicação do método a cada objeto  **Shape** individual naquela coleção.
    
- A configuração do valor de uma propriedade da coleção é equivalente à configuração do valor da propriedade de cada forma individual naquele intervalo.
    
- Uma propriedade da coleção que retorne uma constante retornará o valor da propriedade para uma forma individual na coleção caso todas as formas da coleção tenham o mesmo valor para a propriedade. Se nem todas as formas da coleção tiverem o mesmo valor para a propriedade, ela retornará a constante "mista".
    
- Uma propriedade da coleção que retorne um tipo de dados simples (como  **Long**, **Single** ou **String** ) retornará o valor da propriedade para uma forma individual caso todas as formas da coleção tenham o mesmo valor para a propriedade.
    
- O valor de algumas propriedades só poderá ser retornado ou definido se houver exatamente uma forma na coleção. Se a coleção contiver mais de uma forma, ocorrerá um erro de tempo de execução. Esse geralmente será o caso do retorno ou da configuração de propriedades quando a ação equivalente na interface do usuário for possível apenas com uma única forma (ações como a edição de texto em uma forma ou a edição dos pontos de uma forma livre).
    
As diretrizes anteriores também se aplicarão quando você estiver configurando propriedades de formas agrupadas sob objetos secundários da coleção  **ShapeRange**, como o objeto **FillFormat**. Se o objeto secundário representar operações que possam ser executadas em vários objetos selecionados na interface do usuário, você poderá retornar o objeto de uma coleção **ShapeRange** e definir suas propriedades. Por exemplo, é possível usar a propriedade **Fill** para retornar o objeto **FillFormat** que representa os preenchimentos de todas as formas da coleção **ShapeRange**. A configuração das propriedades desse objeto **FillFormat** definirá as mesmas propriedades para todas as formas individuais da coleção **ShapeRange**.


## Loop em uma coleção Shapes ou ShapeRange

Mesmo se você não conseguir executar uma operação em várias formas ao mesmo tempo na interface do usuário selecionando-as e então usando um comando, será possível executar a ação equivalente programaticamente criando um loop na coleção  **Shapes** ou **ShapeRange** que contenha as formas com que você deseja trabalhar, aplicando as propriedades e os métodos adequados aos objetos **Shape** individuais na coleção. O exemplo a seguir cria um loop em todas as formas de `myDocument` e altera a cor de primeiro plano de cada forma AutoForma.


```
Set myDocument = Worksheets(1) 
For Each sh In myDocument.Shapes 
 If sh.Type = msoAutoShape Then 
 sh.Fill.ForeColor.RGB = RGB(255, 0, 0) 
 End If 
Next
```

O exemplo a seguir cria uma coleção  **ShapeRange** com todas as formas atualmente selecionadas na janela ativa e define a cor de primeiro plano de cada forma selecionada.




```
For Each sh in ActiveWindow.Selection.ShapeRange 
 sh.Fill.ForeColor.RGB = RGB(255, 0, 0) 
Next
```


## Alinhando, distribuindo e agrupando formas em um intervalo de formas

Use os métodos  **[Align](7a4e6442-6730-ab7d-93b5-4c091ada6b14.md)** e **[Distribute](cef14a4b-4d6e-758e-928a-99233f893ddc.md)** para posicionar um conjunto de formas em relação umas às outras ou em relação ao documento que as contém. Use o método **[Group](f0ad9b81-42ad-0ee6-d2e2-ff2a88d47a97.md)** ou o método **[Regroup](d30d3064-c37e-84b0-10a6-11dcd18c593e.md)** para formar uma única forma agrupada a partir de um conjunto de formas.

