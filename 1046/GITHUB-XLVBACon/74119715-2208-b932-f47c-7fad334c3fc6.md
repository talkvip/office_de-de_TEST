
# Consultar intervalos nomeados

Os intervalos são mais fáceis de identificar por nome do que por notação A1. Para nomear um intervalo selecionado, clique na caixa de nome na extremidade esquerda da barra de fórmula, digite um nome e então pressione ENTER.


## Fazendo referência a um intervalo nomeado

O exemplo a seguir se refere ao intervalo chamado "MeuIntervalo" na pasta de trabalho chamada "MinhaPasta.xls".


```
vba
Sub FormatRange() 
    Range("MyBook.xls!MyRange").Font.Italic = True 
End Sub
```

O exemplo a seguir se refere ao intervalo específico da planilha chamada "Planilha1!Vendas" na pasta de trabalho chamada "Relatório.xls".




```
vba
Sub FormatSales() 
    Range("[Report.xls]Sheet1!Sales").BorderAround Weight:=xlthin 
End Sub
```

Para selecionar um intervalo nomeado, use o método  **Go To**, que ativa a pasta de trabalho e a planilha e então seleciona o intervalo.




```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyBook.xls!MyRange" 
    Selection.ClearContents 
End Sub
```

O exemplo a seguir mostra como o mesmo procedimento seria escrito para a pasta de trabalho ativa.




```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyRange" 
    Selection.ClearContents 
End Sub
```

 **Código de exemplo fornecido por:** Dennis Wallentin,[VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)

Este exemplo usa um intervalo nomeado como a fórmula para validação de dados. Este exemplo exige que os dados de validação estejam na Planilha 2 no intervalo A2:A100. Esses dados de validação são usados para validar dados inseridos na Planilha 1 no intervalo D2:D10.




```
vba
Sub Add_Data_Validation_From_Other_Worksheet()
'The current Excel workbook and worksheet, a range to define the data to be validated, and the target range
'to place the data in.
Dim wbBook As Workbook
Dim wsTarget As Worksheet
Dim wsSource As Worksheet
Dim rnTarget As Range
Dim rnSource As Range

'Initialize the Excel objects and delete any artifacts from the last time the macro was run.
Set wbBook = ThisWorkbook
With wbBook
    Set wsSource = .Worksheets("Sheet2")
    Set wsTarget = .Worksheets("Sheet1")
    On Error Resume Next
    .Names("Source").Delete
    On Error GoTo 0
End With

'On the source worksheet, create a range in column A of up to 98 cells long, and name it "Source".
With wsSource
    .Range(.Range("A2"), .Range("A100").End(xlUp)).Name = "Source"
End With

'On the target worksheet, create a range 8 cells long in column D.
Set rnTarget = wsTarget.Range("D2:D10")

'Clear out any artifacts from previous macro runs, then set up the target range with the validation data.
With rnTarget
    .ClearContents
    With .Validation
        .Delete
        .Add Type:=xlValidateList, _
             AlertStyle:=xlValidAlertStop, _
             Formula1:="=Source"
        
'Set up the Error dialog with the appropriate title and message
        .ErrorTitle = "Value Error"
        .ErrorMessage = "You can only choose from the list."
    End With
End With

End Sub
```


## Loop pelas células de um intervalo nomeado

O exemplo a seguir faz um loop por cada célula de um intervalo nomeado usando um loop  **For Each...Next**. Se o valor de cada célula do intervalo exceder o valor de `Limit`, a cor da célula será alterada para amarelo.


```
vba
Sub ApplyColor() 
    Const Limit As Integer = 25 
    For Each c In Range("MyRange") 
        If c.Value > Limit Then 
            c.Interior.ColorIndex = 27 
        End If 
    Next c 
End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

Dennis Wallentin é autor do VSTO &amp; .NET &amp; Excel, um blog que se concentra em soluções .NET Framework para o Excel e os serviços do Excel. Dennis desenvolve soluções do Excel para mais de 20 anos e também é co-autor de "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)."

