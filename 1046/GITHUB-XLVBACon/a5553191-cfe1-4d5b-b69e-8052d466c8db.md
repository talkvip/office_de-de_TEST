
# Nomear uma planilha usando um valor de célula

Este exemplo mostra como nomear uma planilha usando o valor na célula A1 nessa planilha. Este exemplo verifica se o valor na célula A1 é um nome de planilha válido e, se for um nome válido, renomeia a planilha ativa para ser igual ao valor da célula A1 usando a propriedade  **[Name](3d000cdf-5e81-8701-ca7f-bdcce006363b.md)** do objeto **[Worksheet](182b705e-854a-81cc-a4b0-59b942de55ae.md)**.

 **Código de exemplo fornecido por:** Tom Urtis,[Atlas Programming Management](http://www.atlaspm.com/)



```
Private Sub Worksheet_Change(ByVal Target As Range)
    'Specify the target cell whose entry shall be the sheet tab name.
    If Target.Address <> "$A$1" Then Exit Sub
        'If the target cell is empty (contents cleared) then do not change the shet name
    If IsEmpty(Target) Then Exit Sub

    'If the length of the target cell's entry is greater than 31 characters, disallow the entry.
    If Len(Target.Value) > 31 Then
        MsgBox "Worksheet tab names cannot be greater than 31 characters in length." &amp; vbCrLf &amp; _
        "You entered " &amp; Target.Value &amp; ", which has " &amp; Len(Target.Value) &amp; " characters.", , "Keep it under 31 characters"
        Application.EnableEvents = False
        Target.ClearContents
        Application.EnableEvents = True
        Exit Sub
    End If

    'Sheet tab names cannot contain the characters /, \, [, ], *, ?, or :.
    'Verify that none of these characters are present in the cell's entry.
    Dim IllegalCharacter(1 To 7) As String, i As Integer
    IllegalCharacter(1) = "/"
    IllegalCharacter(2) = "\"
    IllegalCharacter(3) = "["
    IllegalCharacter(4) = "]"
    IllegalCharacter(5) = "*"
    IllegalCharacter(6) = "?"
    IllegalCharacter(7) = ":"
    For i = 1 To 7
        If InStr(Target.Value, (IllegalCharacter(i))) > 0 Then
            MsgBox "You used a character that violates sheet naming rules." &amp; vbCrLf &amp; vbCrLf &amp; _
            "Please re-enter a sheet name without the ''" &amp; IllegalCharacter(i) &amp; "'' character.", 48, "Not a possible sheet name !!"
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Exit Sub
        End If
    Next i

    'Verify that the proposed sheet name does not already exist in the workbook.
    Dim strSheetName As String, wks As Worksheet, bln As Boolean
    strSheetName = Trim(Target.Value)
    On Error Resume Next
    Set wks = ActiveWorkbook.Worksheets(strSheetName)
    On Error Resume Next
    If Not wks Is Nothing Then
        bln = True
    Else
        bln = False
        Err.Clear
    End If

    'If the worksheet name does not already exist, name the active sheet as the target cell value.
    'Otherwise, advise the user that duplicate sheet names are not allowed.
    If bln = False Then
        ActiveSheet.Name = strSheetName
    Else
        MsgBox "There is already a sheet named " &amp; strSheetName &amp; "." &amp; vbCrLf &amp; _
        "Please enter a unique name for this sheet."
        Application.EnableEvents = False
        Target.ClearContents
        Application.EnableEvents = True
    End If

End Sub
```


## Sobre o colaborador
<a name="AboutContributor"> </a>

MVP Tom Urtis é o fundador da Atlas Programming Management, uma completa do serviço Microsoft Office e o Excel business solutions empresa no Silicon Valley. João tem mais de 25 anos de experiência em gerenciamento de negócios e desenvolvimento Microsoft Office aplicativos e é co-autor de "Holy Macro! It's 2,500 Excel VBA Examples."

